unforgiven

high

# [High] Function relayMessage() in L1CrossDomainMessenger doesn't check message is not blocked in old version, all the old blocked messages can be relayed even so they could be relayed in the old version and all sort of damage can happen

## Summary
the previous version of the L1CrossDomainMessenger had blocked message functionality and owner could block some messages from getting relayed but the new implementation doesn't check for that the old hash of the message is not blocked and this would give attacker to relay all the old messages that were blocked before the migration. it can cause all sort of damages as the blocked messages were blocked by owner for some important reasons. even if there isn't any current blocked message, there could be some block message between now and the deployment so we can't assume in the time of the deployment there isn't any blocked message and migration can allow those blocked withdrawals.

## Vulnerability Detail
This is the code some part of the `relayMessages()` code in old version of the L1CrossDomainMessenger:
```solidity
    function relayMessage(
        address _target,
        address _sender,
        bytes memory _message,
        uint256 _messageNonce,
        L2MessageInclusionProof memory _proof
    ) public nonReentrant whenNotPaused {
        bytes memory xDomainCalldata = Lib_CrossDomainUtils.encodeXDomainCalldata(
            _target,
            _sender,
            _message,
            _messageNonce
        );

        require(
            _verifyXDomainMessage(xDomainCalldata, _proof) == true,
            "Provided message could not be verified."
        );

        bytes32 xDomainCalldataHash = keccak256(xDomainCalldata);

        require(
            successfulMessages[xDomainCalldataHash] == false,
            "Provided message has already been received."
        );

        require(
            blockedMessages[xDomainCalldataHash] == false,
            "Provided message has been blocked."
        );
....
....
```
As you can see it checks that `blockedMessage[messageHash]` is not true. and owner can block any message by calling `blockMessage()` and unblock it by calling `allowMessage()`. so in the previous design owner could have blocked some malicious withdrawals from L2 to L1. for example if a big hack happens in one of the L2 Defi projects, then owner could have blocked withdrawing those funds.
in the migration process the script convert all the old withdrawal messages from old format to new format and them to the new message database and they can be relayed if they are not relayed in the past. CrossDomainMessage check the old hash format to make sure messages are not relayed in the past and un-relayed old messages L2 to L1 can be relayed after the migration.
but after bedrock update there is no check for the `blockedMessage[]` and old blocked messages which are migrated by the migration script can be relayed again successfully.
attacker can use this to relay old blocked messages and harm the protocol or users. This the new `relayMessages()` code:
```solidity
    function relayMessage(
        uint256 _nonce,
        address _sender,
        address _target,
        uint256 _value,
        uint256 _minGasLimit,
        bytes calldata _message
    ) external payable nonReentrant whenNotPaused {
        (, uint16 version) = Encoding.decodeVersionedNonce(_nonce);
        require(
            version < 2,
            "CrossDomainMessenger: only version 0 or 1 messages are supported at this time"
        );

        // If the message is version 0, then it's a migrated legacy withdrawal. We therefore need
        // to check that the legacy version of the message has not already been relayed.
        if (version == 0) {
            bytes32 oldHash = Hashing.hashCrossDomainMessageV0(_target, _sender, _message, _nonce);
            require(
                successfulMessages[oldHash] == false,
                "CrossDomainMessenger: legacy withdrawal already relayed"
            );
        }

        // We use the v1 message hash as the unique identifier for the message because it commits
        // to the value and minimum gas limit of the message.
        bytes32 versionedHash = Hashing.hashCrossDomainMessageV1(
            _nonce,
            _sender,
            _target,
            _value,
            _minGasLimit,
            _message
        );

        if (_isOtherMessenger()) {
            // These properties should always hold when the message is first submitted (as
            // opposed to being replayed).
            assert(msg.value == _value);
            assert(!failedMessages[versionedHash]);
        } else {
            require(
                msg.value == 0,
                "CrossDomainMessenger: value must be zero unless message is from a system address"
            );

            require(
                failedMessages[versionedHash],
                "CrossDomainMessenger: message cannot be replayed"
            );
        }

        require(
            _isUnsafeTarget(_target) == false,
            "CrossDomainMessenger: cannot send message to blocked system address"
        );

        require(
            successfulMessages[versionedHash] == false,
            "CrossDomainMessenger: message has already been relayed"
        );

        require(
            gasleft() >= _minGasLimit + RELAY_GAS_REQUIRED,
            "CrossDomainMessenger: insufficient gas to relay message"
        );

        xDomainMsgSender = _sender;
        bool success = SafeCall.call(_target, gasleft() - RELAY_GAS_BUFFER, _value, _message);
        xDomainMsgSender = Constants.DEFAULT_L2_SENDER;
.....
```
which as you can see there is no check for blocked messages and old blocked messages can be relayed.
the old withdrawal messages get migrated by migration script which convert CrossDomainMessenger messages from old version to new version and it perform this action for all the messages regardless of them being relayed or un-relayed or blocked or unblocked.
to exploit this issue attacker need to just wait for migration process to be done then call `OptimismPortal.profeWithdrawal()` and `OptimisimPortal.finilizeWithdrawal()` for the old blocked messages and code would check that those messages are in the L2 output and they are not relayed before and it would execute them.

## Impact
all of the old blocked messages by owner can be relayed and cause harm. there may be no blocked messages right now or any serious one but from now until the deployment of the new contract the block message functionality can be used to block some messages and migration can allow attacker to bypass the block mechanism and relay those messages.
So either of migration script or L1CrossDomainMessenger should have handled those blocked withdrawal messages.

## Code Snippet
https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L256-L343

https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts/contracts/L1/messaging/L1CrossDomainMessenger.sol#L106-L119

https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts/contracts/L1/messaging/L1CrossDomainMessenger.sol#L191-L194


## Tool used
Manual Review

## Recommendation
perform one of this 3 action:
1. if there is no blocked message and project want to make sure there won't be anyone, before migration upgrade L1CrossDomainMessenger and remove the block message functionality and this way it would be sure that no blocked messages can be added.
2. in the migration script when migrating the withdrawal messages, don't migrate the old blocked messages. it requires checking the L1 state of the messages during the migration.
3. in the L1CrossDomainMessenger check for old hash format of messages to not be in blockedMessage.