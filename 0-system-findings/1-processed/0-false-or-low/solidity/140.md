
- [140.md](0-system-findings/1-processed/0-false/140.md): #false
  - **Description:** EIP 1559 exponential resource pricing is inaccurate for empty blocks
  - **Reason:** The math strictly follows EIP-1559 style gas parameters to create an elastic gas pricing. Introducing linear decay for empty blocks deviates from the intended behavior of resource metering.

---

GalloDaSballo

medium

# EIP 1559 math is inaccurate by over 2 times for empty blocks

## Summary

The math to account for empty blocks uses exponentiation, when compared to linear math we can see that the output is off by 2x to almost an order of magnitude, meaning that the baseFee for blocks that come after empty blocks is over-priced.

The functions also assume associative property, this report briefly breaks that assumption as well

## Vulnerability Detail

`ResourceMetering` will compute the new fee, and if more than 1 block has expired, it will use an exponential formula to "simulate empty blocks", the below POC compares the result obtained via the exponential math vs linear math.

The linear math would more accurately discount blocks vs the exponential math.

## Impact

Any time a block is skipped, the formula from the in-scope codebase will overprice the gas fees

See the POC below for a comparison between the Exponential Formula and the Linear Formulas

The Linear Formulas simulate calling the function with a 0 usage or the same usage as before.

## Code Snippet

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/ResourceMetering.sol#L112-L128


## Tool used

Manual Review

Foundry / Fuzzing

## POC Base Contract
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {Arithmetic} from "./Arithmetic.sol";

contract FuzzMathBase {
    /**
     * @notice Maximum amount of the resource that can be used within this block.
     */
    int256 public constant MAX_RESOURCE_LIMIT = 8_000_000;

    /**
     * @notice Along with the resource limit, determines the target resource limit.
     */
    int256 public constant ELASTICITY_MULTIPLIER = 4;

    /**
     * @notice Target amount of the resource that should be used within this block.
     */
    int256 public constant TARGET_RESOURCE_LIMIT = MAX_RESOURCE_LIMIT / ELASTICITY_MULTIPLIER;

    /**
     * @notice Denominator that determines max change on fee per block.
     */
    int256 public constant BASE_FEE_MAX_CHANGE_DENOMINATOR = 8;

    /**
     * @notice Minimum base fee value, cannot go lower than this.
     */
    int256 public constant MINIMUM_BASE_FEE = 10_000;

    /**
     * @notice Maximum base fee value, cannot go higher than this.
     */
    int256 public constant MAXIMUM_BASE_FEE = int256(uint256(type(uint128).max));

    /**
     * @notice Initial base fee value.
     */
    uint128 public constant INITIAL_BASE_FEE = 1_000_000_000;

    function withExp(uint256 blockDiff, uint256 prevBoughtGas, uint256 prevBaseFee)
        external
        returns (int256 newBaseFee)
    {
        int256 gasUsedDelta = int256(uint256(prevBoughtGas)) - TARGET_RESOURCE_LIMIT;
        int256 baseFeeDelta =
            (int256(uint256(prevBaseFee)) * gasUsedDelta) / TARGET_RESOURCE_LIMIT / BASE_FEE_MAX_CHANGE_DENOMINATOR;

        // Update base fee by adding the base fee delta and clamp the resulting value between
        // min and max.
        newBaseFee = Arithmetic.clamp(int256(uint256(prevBaseFee)) + baseFeeDelta, MINIMUM_BASE_FEE, MAXIMUM_BASE_FEE);

        // If we skipped more than one block, we also need to account for every empty block.
        // Empty block means there was no demand for deposits in that block, so we should
        // reflect this lack of demand in the fee.
        if (blockDiff > 1) {
            // Update the base fee by repeatedly applying the exponent 1-(1/change_denominator)
            // blockDiff - 1 times. Simulates multiple empty blocks. Clamp the resulting value
            // between min and max.
            newBaseFee = Arithmetic.clamp(
                Arithmetic.cdexp(newBaseFee, BASE_FEE_MAX_CHANGE_DENOMINATOR, int256(blockDiff - 1)),
                MINIMUM_BASE_FEE,
                MAXIMUM_BASE_FEE
            );
        }
    }

    function withLinear(uint256 blockDiff, uint256 prevBoughtGas, uint256 prevBaseFee)
        external
        returns (int256 newBaseFee)
    {
        int256 gasUsedDelta = int256(uint256(prevBoughtGas)) - TARGET_RESOURCE_LIMIT;
        int256 baseFeeDelta =
            (int256(uint256(prevBaseFee)) * gasUsedDelta) / TARGET_RESOURCE_LIMIT / BASE_FEE_MAX_CHANGE_DENOMINATOR;

        // Update base fee by adding the base fee delta and clamp the resulting value between
        // min and max.
        newBaseFee = Arithmetic.clamp(int256(uint256(prevBaseFee)) + baseFeeDelta, MINIMUM_BASE_FEE, MAXIMUM_BASE_FEE);

        // BlockDiff always at least 1 so this happens only at 2+
        for (uint256 i; i < blockDiff - 1; i++) {
            /// @audit We leave prevBoughtGas to simulate the code above
            gasUsedDelta = int256(uint256(prevBoughtGas)) - TARGET_RESOURCE_LIMIT;
            baseFeeDelta =
                (int256(uint256(prevBaseFee)) * gasUsedDelta) / TARGET_RESOURCE_LIMIT / BASE_FEE_MAX_CHANGE_DENOMINATOR;

            newBaseFee =
                Arithmetic.clamp(int256(uint256(prevBaseFee)) + baseFeeDelta, MINIMUM_BASE_FEE, MAXIMUM_BASE_FEE);
        }
    }

    function withZero(uint256 blockDiff, uint256 prevBoughtGas, uint256 prevBaseFee)
        external
        returns (int256 newBaseFee)
    {
        int256 gasUsedDelta = int256(uint256(prevBoughtGas)) - TARGET_RESOURCE_LIMIT;
        int256 baseFeeDelta =
            (int256(uint256(prevBaseFee)) * gasUsedDelta) / TARGET_RESOURCE_LIMIT / BASE_FEE_MAX_CHANGE_DENOMINATOR;

        // Update base fee by adding the base fee delta and clamp the resulting value between
        // min and max.
        newBaseFee = Arithmetic.clamp(int256(uint256(prevBaseFee)) + baseFeeDelta, MINIMUM_BASE_FEE, MAXIMUM_BASE_FEE);

        // BlockDiff always at least 1 so this happens only at 2+
        for (uint256 i; i < blockDiff - 1; i++) {
            /// @audit We set to 0 because there was no usage in these previous blocks
            gasUsedDelta = int256(uint256(0)) - TARGET_RESOURCE_LIMIT;
            baseFeeDelta =
                (int256(uint256(prevBaseFee)) * gasUsedDelta) / TARGET_RESOURCE_LIMIT / BASE_FEE_MAX_CHANGE_DENOMINATOR;

            newBaseFee =
                Arithmetic.clamp(int256(uint256(prevBaseFee)) + baseFeeDelta, MINIMUM_BASE_FEE, MAXIMUM_BASE_FEE);
        }
    }
}
```

## POC - Fuzzing Test
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.15;

import "forge-std/Test.sol";

import {FuzzMathBase} from "src/FuzzMathBase.sol";

contract FuzzTest is Test {
    FuzzMathBase c0;

    function setUp() public {
        c0 = new FuzzMathBase();
    }

    event Debug(string name, int256 value);
    
    // Capped at uint128 to avoid bs overflow
    function testFuzzMathBase(uint128 blockDiff, uint128 prevBoughtGas, uint128 prevBaseFee) public {
        vm.assume(blockDiff < 25);
        vm.assume(blockDiff > 3);
        vm.assume(prevBoughtGas > 0);
        vm.assume(prevBaseFee > 0);
        vm.assume(prevBaseFee > 1e9);
        vm.assume(prevBoughtGas < 8_000_000);
        
        int256 withExp = c0.withExp(blockDiff, prevBoughtGas, prevBaseFee);
        int256 withLinear = c0.withLinear(blockDiff, prevBoughtGas, prevBaseFee);
        int256 withZero = c0.withZero(blockDiff, prevBoughtGas, prevBaseFee);

        emit Debug("withExp", withExp);
        emit Debug("withLinear", withLinear);
        emit Debug("withZero", withZero);

        assertEq(withExp, withLinear);
    }
}
```

## Example output

```bash
[FAIL. Reason: Assertion failed. Counterexample: calldata=0xd4371f2e0000000000000000000000000000000000000000000000000000000000000011000000000000000000000000000000000000000000000000000000000000014b0000000000000000000000000000000000000001c17220a7845dfc73aae54597, args=[17, 331, 139096741159091772881044325783]] testFuzzMathBase(uint128,uint128,uint128) (runs: 0, μ: 0, ~: 0)
Logs:
  Error: a == b not satisfied [int]
    Expected: 121712526078038029981965261665
      Actual: 14370243408031729147328632925

Traces:
  [83717] FuzzTest::testFuzzMathBase(17, 331, 139096741159091772881044325783) 
    ├─ [0] VM::assume(true) 
    │   └─ ← ()
    ├─ [0] VM::assume(true) 
    │   └─ ← ()
    ├─ [0] VM::assume(true) 
    │   └─ ← ()
    ├─ [0] VM::assume(true) 
    │   └─ ← ()
    ├─ [0] VM::assume(true) 
    │   └─ ← ()
    ├─ [0] VM::assume(true) 
    │   └─ ← ()
    ├─ [4630] FuzzMathBase::withExp(17, 331, 139096741159091772881044325783) 
    │   └─ ← 14370243408031729147328632925
    ├─ [24774] FuzzMathBase::withLinear(17, 331, 139096741159091772881044325783) 
    │   └─ ← 121712526078038029981965261665
    ├─ [24818] FuzzMathBase::withZero(17, 331, 139096741159091772881044325783) 
    │   └─ ← 121709648514205301270913785061
    ├─ emit Debug(name: withExp, value: 14370243408031729147328632925)
    ├─ emit Debug(name: withLinear, value: 121712526078038029981965261665)
    ├─ emit Debug(name: withZero, value: 121709648514205301270913785061)
    ├─ emit log(: Error: a == b not satisfied [int])
    ├─ emit log_named_int(key:   Expected, val: 121712526078038029981965261665)
    ├─ emit log_named_int(key:     Actual, val: 14370243408031729147328632925)
    ├─ [0] VM::store(VM: [0x7109709ECfa91a80626fF3989D68f67F5b1DD12D], 0x6661696c65640000000000000000000000000000000000000000000000000000, 0x0000000000000000000000000000000000000000000000000000000000000001) 
    │   └─ ← ()
    └─ ← ()

Test result: FAILED. 0 passed; 1 failed; finished in 10.63ms

Failing tests:
Encountered 1 failing test in test/FuzzTest.sol:FuzzTest
[FAIL. Reason: Assertion failed. Counterexample: calldata=0xd4371f2e0000000000000000000000000000000000000000000000000000000000000011000000000000000000000000000000000000000000000000000000000000014b0000000000000000000000000000000000000001c17220a7845dfc73aae54597, args=[17, 331, 139096741159091772881044325783]] testFuzzMathBase(uint128,uint128,uint128) (runs: 0, μ: 0, ~: 0)

Encountered a total of 1 failing tests, 0 tests succeeded
```


## POC - Interpretation

You can see in this POC that the exponential math is 8 times higher in this specific instance

In general the math is off by almost 50% (can be proven by running the above POC in foundry)

## Recommendation

Use a linear formula, or find a formula that better reflects the lack of usage (which would require pricing empty blocks at a negative value, not at the latest used value)

Additionally, consider changing the order of operations unless you believe the difference to be marginal

## Further Considerations on Associative Property of operations

The math first computes the cost of the next block, and then "catches up" with the older blocks

In theory this operation should be inverted:
- First the math should catchup
- Then the math computes the new base fee

This will ensure the latest math is applied to the latest block.

Intuition of how this can result in incorrect math (by absurd):

- Assume we have enough operations to bring the fee to the MIN (Operation A) 
- The next operation will bring the fee above the MIN (MIN + Any > MIN) (Operation B)

If we assume associative property, then the order of operations wouldn't matter.

However, it does:

If we perform A before B, we will have a base fee of MIN + Value(B)
If we perform B before A (this is the case in the code in-scope), we will get MIN

By absurd we demonstrated that the Associative Property doesn't hold, and that the math should be swapped (catch-up from old values first, apply new prices next)


## Additional Resources

For the sake of full transparency am adding the library files to allow full reproduction


OldSolmatemath.sol
```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

/// @notice Arithmetic library with operations for fixed-point numbers.
/// @author Solmate (https://github.com/Rari-Capital/solmate/blob/main/src/utils/FixedPointMathLib.sol)
library FixedPointMathLib {
    /*//////////////////////////////////////////////////////////////
                    SIMPLIFIED FIXED POINT OPERATIONS
    //////////////////////////////////////////////////////////////*/

    uint256 internal constant WAD = 1e18; // The scalar of ETH and most ERC20s.

    function mulWadDown(uint256 x, uint256 y) internal pure returns (uint256) {
        return mulDivDown(x, y, WAD); // Equivalent to (x * y) / WAD rounded down.
    }

    function mulWadUp(uint256 x, uint256 y) internal pure returns (uint256) {
        return mulDivUp(x, y, WAD); // Equivalent to (x * y) / WAD rounded up.
    }

    function divWadDown(uint256 x, uint256 y) internal pure returns (uint256) {
        return mulDivDown(x, WAD, y); // Equivalent to (x * WAD) / y rounded down.
    }

    function divWadUp(uint256 x, uint256 y) internal pure returns (uint256) {
        return mulDivUp(x, WAD, y); // Equivalent to (x * WAD) / y rounded up.
    }

    function powWad(int256 x, int256 y) internal pure returns (int256) {
        // Equivalent to x to the power of y because x ** y = (e ** ln(x)) ** y = e ** (ln(x) * y)
        return expWad((lnWad(x) * y) / int256(WAD)); // Using ln(x) means x must be greater than 0.
    }

    function expWad(int256 x) internal pure returns (int256 r) {
        unchecked {
            // When the result is < 0.5 we return zero. This happens when
            // x <= floor(log(0.5e18) * 1e18) ~ -42e18
            if (x <= -42139678854452767551) return 0;

            // When the result is > (2**255 - 1) / 1e18 we can not represent it as an
            // int. This happens when x >= floor(log((2**255 - 1) / 1e18) * 1e18) ~ 135.
            if (x >= 135305999368893231589) revert("EXP_OVERFLOW");

            // x is now in the range (-42, 136) * 1e18. Convert to (-42, 136) * 2**96
            // for more intermediate precision and a binary basis. This base conversion
            // is a multiplication by 1e18 / 2**96 = 5**18 / 2**78.
            x = (x << 78) / 5 ** 18;

            // Reduce range of x to (-½ ln 2, ½ ln 2) * 2**96 by factoring out powers
            // of two such that exp(x) = exp(x') * 2**k, where k is an integer.
            // Solving this gives k = round(x / log(2)) and x' = x - k * log(2).
            int256 k = ((x << 96) / 54916777467707473351141471128 + 2 ** 95) >> 96;
            x = x - k * 54916777467707473351141471128;

            // k is in the range [-61, 195].

            // Evaluate using a (6, 7)-term rational approximation.
            // p is made monic, we'll multiply by a scale factor later.
            int256 y = x + 1346386616545796478920950773328;
            y = ((y * x) >> 96) + 57155421227552351082224309758442;
            int256 p = y + x - 94201549194550492254356042504812;
            p = ((p * y) >> 96) + 28719021644029726153956944680412240;
            p = p * x + (4385272521454847904659076985693276 << 96);

            // We leave p in 2**192 basis so we don't need to scale it back up for the division.
            int256 q = x - 2855989394907223263936484059900;
            q = ((q * x) >> 96) + 50020603652535783019961831881945;
            q = ((q * x) >> 96) - 533845033583426703283633433725380;
            q = ((q * x) >> 96) + 3604857256930695427073651918091429;
            q = ((q * x) >> 96) - 14423608567350463180887372962807573;
            q = ((q * x) >> 96) + 26449188498355588339934803723976023;

            assembly {
                // Div in assembly because solidity adds a zero check despite the unchecked.
                // The q polynomial won't have zeros in the domain as all its roots are complex.
                // No scaling is necessary because p is already 2**96 too large.
                r := sdiv(p, q)
            }

            // r should be in the range (0.09, 0.25) * 2**96.

            // We now need to multiply r by:
            // * the scale factor s = ~6.031367120.
            // * the 2**k factor from the range reduction.
            // * the 1e18 / 2**96 factor for base conversion.
            // We do this all at once, with an intermediate result in 2**213
            // basis, so the final right shift is always by a positive amount.
            r = int256((uint256(r) * 3822833074963236453042738258902158003155416615667) >> uint256(195 - k));
        }
    }

    function lnWad(int256 x) internal pure returns (int256 r) {
        unchecked {
            require(x > 0, "UNDEFINED");

            // We want to convert x from 10**18 fixed point to 2**96 fixed point.
            // We do this by multiplying by 2**96 / 10**18. But since
            // ln(x * C) = ln(x) + ln(C), we can simply do nothing here
            // and add ln(2**96 / 10**18) at the end.

            // Reduce range of x to (1, 2) * 2**96
            // ln(2^k * x) = k * ln(2) + ln(x)
            int256 k = int256(log2(uint256(x))) - 96;
            x <<= uint256(159 - k);
            x = int256(uint256(x) >> 159);

            // Evaluate using a (8, 8)-term rational approximation.
            // p is made monic, we will multiply by a scale factor later.
            int256 p = x + 3273285459638523848632254066296;
            p = ((p * x) >> 96) + 24828157081833163892658089445524;
            p = ((p * x) >> 96) + 43456485725739037958740375743393;
            p = ((p * x) >> 96) - 11111509109440967052023855526967;
            p = ((p * x) >> 96) - 45023709667254063763336534515857;
            p = ((p * x) >> 96) - 14706773417378608786704636184526;
            p = p * x - (795164235651350426258249787498 << 96);

            // We leave p in 2**192 basis so we don't need to scale it back up for the division.
            // q is monic by convention.
            int256 q = x + 5573035233440673466300451813936;
            q = ((q * x) >> 96) + 71694874799317883764090561454958;
            q = ((q * x) >> 96) + 283447036172924575727196451306956;
            q = ((q * x) >> 96) + 401686690394027663651624208769553;
            q = ((q * x) >> 96) + 204048457590392012362485061816622;
            q = ((q * x) >> 96) + 31853899698501571402653359427138;
            q = ((q * x) >> 96) + 909429971244387300277376558375;
            assembly {
                // Div in assembly because solidity adds a zero check despite the unchecked.
                // The q polynomial is known not to have zeros in the domain.
                // No scaling required because p is already 2**96 too large.
                r := sdiv(p, q)
            }

            // r is in the range (0, 0.125) * 2**96

            // Finalization, we need to:
            // * multiply by the scale factor s = 5.549…
            // * add ln(2**96 / 10**18)
            // * add k * ln(2)
            // * multiply by 10**18 / 2**96 = 5**18 >> 78

            // mul s * 5e18 * 2**96, base is now 5**18 * 2**192
            r *= 1677202110996718588342820967067443963516166;
            // add ln(2) * k * 5e18 * 2**192
            r += 16597577552685614221487285958193947469193820559219878177908093499208371 * k;
            // add ln(2**96 / 10**18) * 5e18 * 2**192
            r += 600920179829731861736702779321621459595472258049074101567377883020018308;
            // base conversion: mul 2**18 / 2**192
            r >>= 174;
        }
    }

    /*//////////////////////////////////////////////////////////////
                    LOW LEVEL FIXED POINT OPERATIONS
    //////////////////////////////////////////////////////////////*/

    function mulDivDown(uint256 x, uint256 y, uint256 denominator) internal pure returns (uint256 z) {
        assembly {
            // Store x * y in z for now.
            z := mul(x, y)

            // Equivalent to require(denominator != 0 && (x == 0 || (x * y) / x == y))
            if iszero(and(iszero(iszero(denominator)), or(iszero(x), eq(div(z, x), y)))) { revert(0, 0) }

            // Divide z by the denominator.
            z := div(z, denominator)
        }
    }

    function mulDivUp(uint256 x, uint256 y, uint256 denominator) internal pure returns (uint256 z) {
        assembly {
            // Store x * y in z for now.
            z := mul(x, y)

            // Equivalent to require(denominator != 0 && (x == 0 || (x * y) / x == y))
            if iszero(and(iszero(iszero(denominator)), or(iszero(x), eq(div(z, x), y)))) { revert(0, 0) }

            // First, divide z - 1 by the denominator and add 1.
            // We allow z - 1 to underflow if z is 0, because we multiply the
            // end result by 0 if z is zero, ensuring we return 0 if z is zero.
            z := mul(iszero(iszero(z)), add(div(sub(z, 1), denominator), 1))
        }
    }

    function rpow(uint256 x, uint256 n, uint256 scalar) internal pure returns (uint256 z) {
        assembly {
            switch x
            case 0 {
                switch n
                case 0 {
                    // 0 ** 0 = 1
                    z := scalar
                }
                default {
                    // 0 ** n = 0
                    z := 0
                }
            }
            default {
                switch mod(n, 2)
                case 0 {
                    // If n is even, store scalar in z for now.
                    z := scalar
                }
                default {
                    // If n is odd, store x in z for now.
                    z := x
                }

                // Shifting right by 1 is like dividing by 2.
                let half := shr(1, scalar)

                for {
                    // Shift n right by 1 before looping to halve it.
                    n := shr(1, n)
                } n {
                    // Shift n right by 1 each iteration to halve it.
                    n := shr(1, n)
                } {
                    // Revert immediately if x ** 2 would overflow.
                    // Equivalent to iszero(eq(div(xx, x), x)) here.
                    if shr(128, x) { revert(0, 0) }

                    // Store x squared.
                    let xx := mul(x, x)

                    // Round to the nearest number.
                    let xxRound := add(xx, half)

                    // Revert if xx + half overflowed.
                    if lt(xxRound, xx) { revert(0, 0) }

                    // Set x to scaled xxRound.
                    x := div(xxRound, scalar)

                    // If n is even:
                    if mod(n, 2) {
                        // Compute z * x.
                        let zx := mul(z, x)

                        // If z * x overflowed:
                        if iszero(eq(div(zx, x), z)) {
                            // Revert if x is non-zero.
                            if iszero(iszero(x)) { revert(0, 0) }
                        }

                        // Round to the nearest number.
                        let zxRound := add(zx, half)

                        // Revert if zx + half overflowed.
                        if lt(zxRound, zx) { revert(0, 0) }

                        // Return properly scaled zxRound.
                        z := div(zxRound, scalar)
                    }
                }
            }
        }
    }

    /*//////////////////////////////////////////////////////////////
                        GENERAL NUMBER UTILITIES
    //////////////////////////////////////////////////////////////*/

    function sqrt(uint256 x) internal pure returns (uint256 z) {
        assembly {
            let y := x // We start y at x, which will help us make our initial estimate.

            z := 181 // The "correct" value is 1, but this saves a multiplication later.

            // This segment is to get a reasonable initial estimate for the Babylonian method. With a bad
            // start, the correct # of bits increases ~linearly each iteration instead of ~quadratically.

            // We check y >= 2^(k + 8) but shift right by k bits
            // each branch to ensure that if x >= 256, then y >= 256.
            if iszero(lt(y, 0x10000000000000000000000000000000000)) {
                y := shr(128, y)
                z := shl(64, z)
            }
            if iszero(lt(y, 0x1000000000000000000)) {
                y := shr(64, y)
                z := shl(32, z)
            }
            if iszero(lt(y, 0x10000000000)) {
                y := shr(32, y)
                z := shl(16, z)
            }
            if iszero(lt(y, 0x1000000)) {
                y := shr(16, y)
                z := shl(8, z)
            }

            // Goal was to get z*z*y within a small factor of x. More iterations could
            // get y in a tighter range. Currently, we will have y in [256, 256*2^16).
            // We ensured y >= 256 so that the relative difference between y and y+1 is small.
            // That's not possible if x < 256 but we can just verify those cases exhaustively.

            // Now, z*z*y <= x < z*z*(y+1), and y <= 2^(16+8), and either y >= 256, or x < 256.
            // Correctness can be checked exhaustively for x < 256, so we assume y >= 256.
            // Then z*sqrt(y) is within sqrt(257)/sqrt(256) of sqrt(x), or about 20bps.

            // For s in the range [1/256, 256], the estimate f(s) = (181/1024) * (s+1) is in the range
            // (1/2.84 * sqrt(s), 2.84 * sqrt(s)), with largest error when s = 1 and when s = 256 or 1/256.

            // Since y is in [256, 256*2^16), let a = y/65536, so that a is in [1/256, 256). Then we can estimate
            // sqrt(y) using sqrt(65536) * 181/1024 * (a + 1) = 181/4 * (y + 65536)/65536 = 181 * (y + 65536)/2^18.

            // There is no overflow risk here since y < 2^136 after the first branch above.
            z := shr(18, mul(z, add(y, 65536))) // A mul() is saved from starting z at 181.

            // Given the worst case multiplicative error of 2.84 above, 7 iterations should be enough.
            z := shr(1, add(z, div(x, z)))
            z := shr(1, add(z, div(x, z)))
            z := shr(1, add(z, div(x, z)))
            z := shr(1, add(z, div(x, z)))
            z := shr(1, add(z, div(x, z)))
            z := shr(1, add(z, div(x, z)))
            z := shr(1, add(z, div(x, z)))

            // If x+1 is a perfect square, the Babylonian method cycles between
            // floor(sqrt(x)) and ceil(sqrt(x)). This statement ensures we return floor.
            // See: https://en.wikipedia.org/wiki/Integer_square_root#Using_only_integer_division
            // Since the ceil is rare, we save gas on the assignment and repeat division in the rare case.
            // If you don't care whether the floor or ceil square root is returned, you can remove this statement.
            z := sub(z, lt(div(x, z), z))
        }
    }

    function log2(uint256 x) internal pure returns (uint256 r) {
        require(x > 0, "UNDEFINED");

        assembly {
            r := shl(7, lt(0xffffffffffffffffffffffffffffffff, x))
            r := or(r, shl(6, lt(0xffffffffffffffff, shr(r, x))))
            r := or(r, shl(5, lt(0xffffffff, shr(r, x))))
            r := or(r, shl(4, lt(0xffff, shr(r, x))))
            r := or(r, shl(3, lt(0xff, shr(r, x))))
            r := or(r, shl(2, lt(0xf, shr(r, x))))
            r := or(r, shl(1, lt(0x3, shr(r, x))))
            r := or(r, lt(0x1, shr(r, x)))
        }
    }

    function unsafeMod(uint256 x, uint256 y) internal pure returns (uint256 z) {
        assembly {
            // z will equal 0 if y is 0, unlike in Solidity where it will revert.
            z := mod(x, y)
        }
    }

    function unsafeDiv(uint256 x, uint256 y) internal pure returns (uint256 z) {
        assembly {
            // z will equal 0 if y is 0, unlike in Solidity where it will revert.
            z := div(x, y)
        }
    }

    /// @dev Will return 0 instead of reverting if y is zero.
    function unsafeDivUp(uint256 x, uint256 y) internal pure returns (uint256 z) {
        assembly {
            // Add 1 to x * y if x % y > 0.
            z := add(gt(mod(x, y), 0), div(x, y))
        }
    }
}

```

Arithmetic.sol
```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

import {FixedPointMathLib} from "./OldSolmatemath.sol";
import {SignedMath} from "@openzeppelin/contracts/utils/math/SignedMath.sol";

/**
 * @title Arithmetic
 * @notice Even more math than before.
 */
library Arithmetic {
    /**
     * @notice Clamps a value between a minimum and maximum.
     *
     * @param _value The value to clamp.
     * @param _min   The minimum value.
     * @param _max   The maximum value.
     *
     * @return The clamped value.
     */
    function clamp(int256 _value, int256 _min, int256 _max) internal pure returns (int256) {
        return SignedMath.min(SignedMath.max(_value, _min), _max);
    }

    /**
     * @notice (c)oefficient (d)enominator (exp)onentiation function.
     *         Returns the result of: c * (1 - 1/d)^exp.
     *
     * @param _coefficient Coefficient of the function.
     * @param _denominator Fractional denominator.
     * @param _exponent    Power function exponent.
     *
     * @return Result of c * (1 - 1/d)^exp.
     */
    function cdexp(int256 _coefficient, int256 _denominator, int256 _exponent) internal pure returns (int256) {
        return (_coefficient * (FixedPointMathLib.powWad(1e18 - (1e18 / _denominator), _exponent * 1e18))) / 1e18;
    }
}

```