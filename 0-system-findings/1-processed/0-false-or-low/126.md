
- [126.md](0-system-findings/1-processed/126.md): #false
  - **Description:** Weird tokens
  - **Reason:** Deflationary and weird tokens are explicitly not supported by the Standard Bridge per a comment in the code.

---

NotFound

high

# L1 insolvency due to some ERC20 tokens

## Summary
There are ERC20 tokens that may make certain customizations to their ERC20 contracts. One type of these tokens is deflationary tokens that charge a certain fee for  very  `transfer()`  or  `transferFrom()`. Others are rebasing tokens that increase in value over time like Aave's aTokens (`balanceOf` changes over time).

## Vulnerability Detail
### Case 1 (fee-on-transfer):

- The user deposits 500 of  a fee-on-transfer token on the `L1StandarBridge`
- The user gets credit for 500 tokens (gets 500 respective tokens minted on L2) while the `L1StandarBridge` contract gets the 500 - fee
- The user withdraws 500 leaving the contract with the difference in the fee
- In the case that there’s more liquidity of that token on the bridge, the user is effectively stealing other users’ tokens; in the case that the user is the last one withdrawing the bridge for that token would be bricked.

### Case 2 (rebasing):

- The user deposits 500 of  a rebasing (e. stETH) token on the `L1StandarBridge`
- The user gets credit for 500 tokens (gets 500 respective tokens minted on L2) while the `L1StandarBridge` contract doesn’t get exactly 500 tokens
- Each minute the user has the token deposited in the bridge, he is losing rewards, that are getting locked forever in the `L1StandarBridge`, because the token balances mapping on the bridge is not being updated.
- When withdrawing, the user will receive the 500 tokens he staked, but he will lose the rewards

## Impact
An attacker may be able to keep calling `depositERC20()` and `withdraw()` while being credited more tokens than the contract actually receives eventually draining it. For rebasing tokens the impact relies on the users not being able to collect rewards or supply changes when they have their tokens deposited in the bridge.

## PoC
Proof of concept tests written in Foundry

```solidity
function testDepositUpdatesWrongForFeeOnTransferTokens() public {
  //bob is an address that already holds PAXG, a fee on transfer token
  startHoax(bob);
  PAXG.approve(address(l1StandardBridge), amount);

  //PAXG balance of l1StandardBridge before deposit
  uint256 balanceBefore = PAXG.balanceOf(address(l1StandardBridge));
  l1StandardBridge.depositERC20(address(PAXG), address(PAXG), amount, 1000000, "");
  //PAXG balance of l1StandardBridge after deposit
  uint256 balanceAfter = PAXG.balanceOf(address(l1StandardBridge));

  //assert amount is greater than the real balance transferred
  assertGt(amount, balanceAfter - balanceBefore);
}

function testDepositUpdatesWrongForRebasingTokens() public {
  //alice is an address that already holds stETH
  startHoax(alice);
  stETH.approve(address(l1StandardBridge), amount);

  //stETH balance of l1StandardBridge before deposit
  uint256 balanceBefore = stETH.balanceOf(address(l1StandardBridge));
  l1StandardBridge.depositERC20(address(stETH), address(stETH), amount, 1000000, "");
  //stETH balance of l1StandardBridge after deposit
  uint256 balanceAfter = stETH.balanceOf(address(l1StandardBridge));

  //assert amount is different from the balance transferred 
  assertTrue(amount != balanceAfter - balanceBefore);
}
```

## Code Snippet
https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1StandardBridge.sol#L173-L189

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1StandardBridge.sol#L204-L221

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1StandardBridge.sol#L281-L292

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L397-L436


## Tool used

Manual Review, Foundry

## Recommendation
If the bridge contract is not intended to support all variations of ERC20 tokens, it is recommended to verify if the value that the bridge receives after the transfer is equal to the amount transferred by the user before accepting the deposit:

```diff
function _initiateBridgeERC20(
  address _localToken,
  address _remoteToken,
  address _from,
  address _to,
  uint256 _amount,
  uint32 _minGasLimit,
  bytes calldata _extraData
) internal {
  if (_isOptimismMintableERC20(_localToken)) {
      require(
          _isCorrectTokenPair(_localToken, _remoteToken),
          "StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
      );

      OptimismMintableERC20(_localToken).burn(_from, _amount);
  } else {
+		uint256 balanceBefore = IERC20(_localToken).balanceOf(address(this));
      IERC20(_localToken).safeTransferFrom(_from, address(this), _amount);
+		uint256 balanceAfter = IERC20(_localToken).balanceOf(address(this));    
+	  	require((balanceAfter - balanceBefore) == _amount, "ERC20 variations not supported");
		deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] + _amount;
}

  emit ERC20BridgeInitiated(_localToken, _remoteToken, _from, _to, _amount, _extraData);

  MESSENGER.sendMessage(
      address(OTHER_BRIDGE),
      abi.encodeWithSelector(
          this.finalizeBridgeERC20.selector,
          // Because this call will be executed on the remote chain, we reverse the order of
          // the remote and local token addresses relative to their order in the
          // finalizeBridgeERC20 function.
          _remoteToken,
          _localToken,
          _from,
          _to,
          _amount,
          _extraData
      ),
      _minGasLimit
  );
}

```
