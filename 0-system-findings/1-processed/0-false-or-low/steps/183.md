hyh

low

# SystemDictator will be stuck after exit1()

## Summary

exit1() leaves SystemDictator in a conflicting state and a continuation of the migration will be impossible with current contract instance, i.e. it will require contract redeployment.

## Vulnerability Detail

Suppose something went wrong and `step1 -> step2 -> exit1` call sequence was executed.

After exit1() concludes, `currentStep` remains to be `EXIT_1_NO_RETURN_STEP == 3`.

However, exit1() directly cancels step2() by setting `OVM_L1CrossDomainMessenger` and `DTL_SHUTOFF_BLOCK` back, so when it will be needed to continue the deployment, step2() will be necessary to run, but it will be reverted as `currentStep` is `3` after exit1().

## Impact

SystemDictator will become unusable after exit1() was called for any reason as there is no way to reset `currentStep`, so no way to repeat step2(), while further steps cannot be carried out with legacy system still running.

## Code Snippet

exit1() directly cancels step2(), but leaves `currentStep == 3`, so step2() cannot be replayed:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/deployment/SystemDictator.sol#L408-L425

```solidity
    /**
     * @notice First exit point, can only be called before step 3 is executed.
     */
    function exit1() external onlyOwner {
        require(
            currentStep == EXIT_1_NO_RETURN_STEP,
            "SystemDictator: can only exit1 before step 3 is executed"
        );

        // Reset the L1CrossDomainMessenger to the old implementation.
        config.globalConfig.addressManager.setAddress(
            "OVM_L1CrossDomainMessenger",
            oldL1CrossDomainMessenger
        );

        // Unset the DTL shutoff block which will allow the DTL to sync again.
        config.globalConfig.addressManager.setAddress("DTL_SHUTOFF_BLOCK", address(0));
    }
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/deployment/SystemDictator.sol#L193-L217

```solidity
    /**
     * @notice Pauses the system by shutting down the L1CrossDomainMessenger and setting the
     *         deposit halt flag to tell the Sequencer's DTL to stop accepting deposits.
     */
    function step2() external onlyOwner step(2) {
        // Store the address of the old L1CrossDomainMessenger implementation. We will need this
        // address in the case that we have to exit early.
        oldL1CrossDomainMessenger = config.globalConfig.addressManager.getAddress(
            "OVM_L1CrossDomainMessenger"
        );

        // Temporarily brick the L1CrossDomainMessenger by setting its implementation address to
        // address(0) which will cause the ResolvedDelegateProxy to revert. Better than pausing
        // the L1CrossDomainMessenger via pause() because it can be easily reverted.
        config.globalConfig.addressManager.setAddress("OVM_L1CrossDomainMessenger", address(0));

        // Set the DTL shutoff block, which will tell the DTL to stop syncing new deposits from the
        // CanonicalTransactionChain. We do this by setting an address in the AddressManager
        // because the DTL already has a reference to the AddressManager and this way we don't also
        // need to give it a reference to the SystemDictator.
        config.globalConfig.addressManager.setAddress(
            "DTL_SHUTOFF_BLOCK",
            address(uint160(block.number))
        );
    }
```

## Tool used

Manual Review

## Recommendation

Consider restoring `currentStep` as well, so the migration can be continued:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/deployment/SystemDictator.sol#L408-L425

```solidity
    /**
     * @notice First exit point, can only be called before step 3 is executed.
     */
    function exit1() external onlyOwner {
        require(
            currentStep == EXIT_1_NO_RETURN_STEP,
            "SystemDictator: can only exit1 before step 3 is executed"
        );

        // Reset the L1CrossDomainMessenger to the old implementation.
        config.globalConfig.addressManager.setAddress(
            "OVM_L1CrossDomainMessenger",
            oldL1CrossDomainMessenger
        );

        // Unset the DTL shutoff block which will allow the DTL to sync again.
        config.globalConfig.addressManager.setAddress("DTL_SHUTOFF_BLOCK", address(0));

+       currentStep = 2;
    }
```

Also, for flexibility purposes consider introducing direct onlyOwner setting of `currentStep`, for example:

```solidity
+   // Resetting the step, use with care
+   function resetStep(uint8 currentStep_) external onlyOwner {
+       currentStep = currentStep_;
+   }
```