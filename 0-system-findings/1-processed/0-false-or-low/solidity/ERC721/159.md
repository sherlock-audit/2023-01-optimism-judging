
- [159.md](0-system-findings/1-processed/0-false/159.md): #false
  - **Description:** ERC721 safeTransferFrom could contain a hook allowing tokens to be flashloaned on withdrawal from L2 -> L1.
  - **Reason:** The assumption of a hook function inside the ERC721 is not an issue indemnent with Optimism's bridge. This logic is outside the scope and does not break any bridge assumptions.

---

unforgiven

medium

# [Medium] It's possible to take locked NFTs in contract L1ERC721Bridge as flash loan and steal their airdrops

## Summary
Contract L1ERC721Bridge is responsible for locking up the NFTs in the L1 and sending message to L2 to bridge the tokens and also sending locked tokens to users when they bridge back tokens from L2. But function `_initiateBridgeERC721()` set the value of the `deposits[][]` and then tries to transfer the NFT with external call, so during the external call the state is not right and if NFT token had a hook function, attacker can use this wrong state to withdraw the NFT with `finalizeBridgeERC721()` (by bridge message) and use it to get airdrops or rewards and then return it in the rest of the `_initiateBridgeERC721()`.

## Vulnerability Detail
This is `_initiateBridgeERC721()` code:
```solidity
    function _initiateBridgeERC721(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _tokenId,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) internal override {
        require(_remoteToken != address(0), "ERC721Bridge: remote token cannot be address(0)");

        // Construct calldata for _l2Token.finalizeBridgeERC721(_to, _tokenId)
        bytes memory message = abi.encodeWithSelector(
            L2ERC721Bridge.finalizeBridgeERC721.selector,
            _remoteToken,
            _localToken,
            _from,
            _to,
            _tokenId,
            _extraData
        );

        // Lock token into bridge
        deposits[_localToken][_remoteToken][_tokenId] = true;
        IERC721(_localToken).transferFrom(_from, address(this), _tokenId);

        // Send calldata into L2
        MESSENGER.sendMessage(OTHER_BRIDGE, message, _minGasLimit);
        emit ERC721BridgeInitiated(_localToken, _remoteToken, _from, _to, _tokenId, _extraData);
    }
```
As you can see it first set the value of the `deposits[localtoken][remotetoken][id]` and then tries to transfer the NFT token from user. so if NFT token had some hook function which calls the `from` address (for example for checking transfer permission or custom NFT token in a project) then the execution would reach into `from` address and it can perform malicious actions as `deposits[localtoken][remotetoken]id` is true. to exploit this attacker would perform this steps:
1. USER1 bridged his id=10 NFT1 from L1 to L2 address NFT2 (legit L2 bridged token for NFT1).
2. id=10 NFT1 has some rewards staked in the Project1 (for example airdrop) which is received by calling `Project1.getReward()` when owning the id=10 NFT1.
3. to steal id=10 NFT1 rewards first attacker would create a malicious token NFT3 in L2 which is IOptimismMintableERC721 and the return value of the `IOptimismMintableERC721(_localToken).remoteToken()` is NFT1 address and attacker would mint id=10 NFT3 to his own address.
4. then attacker would call `L2ERC721Bridge.bridgeERC721(NFT3 as local token, NFT1 as remote token, attackerContract as to, 10 as id,)` and code would check that NFT3 and NFT1 is valid pair and would send a message to L1ERC721Bridge to withdraw id=10 NFT1 for NFT3 token and that message would be send over the bridge to L1 network.
5. attacker would wait so the message would reach to L2 oracle output and then send the prove of the withdrawal and wait for the withdrawal delay.
6. after withdrawal delay passed attacker would perform the final attack to get id=10 NFT1 as flash loan by creating a contract and perform this:
7. first attacker contract would call `L1ERC721Bridge.bridgeERC721To(NFT1, NFT3, attackerAddress, 10)` and code would set `deposits[NFT1][NFT3][10]` as True and then code would try to transfer the id=1 NFT1 token from attacker address, and NFT1 would call hook function of the attacker contract(we assume the NFT1 token has hook function, for checking permision or some custom NFT for a project).
8. attacker contract hook function would call `OptimismPortal.finilizeWithdraw()` and would finalize the withdraw message that L2ERC721Brige sent to L1ERC721Bridge.
9. L1ERC721Bridge would receive the withdrawal of the (id=10 NFT1 in L1 for NFT3 in L2) message and would check that that `deposits[NFT1][NFT3][10]` is true and would send the id=10 NFT1 to attacker contract address.
10. then attacker would use the id=10 NFT1 and call the `Project.getRewards()` and steal the NFT rewards and then the `finilizeWithdrawal()` and hook function would returns.
11. then L1ERC721Bridge would transfer id=10 NFT1 from attacker address to contract address.

so by performing this attack, attacker was able to get locked NFTs in the L1 bridge as flash loan without paying any fee and also attacker was able to steal any reward or airdrops for those NFTs without being the real owner of them or without becoming the owner of those bridged NFTs in the L2. of course we assumed that NFT token as some hook function when calling `safeTransferFrom()` which calls `from` or another attacker controller address. this assumption can be true in some cases because this is general bridge and it supports all the NFTs and users and project would bridge all kind of NFT tokens which belongs to different projects.
attacker can perform this attack for all the ids of NFTs that are locked up and steal a lot of airdrops or rewards.

## Impact
Attacker can steal airdrops or other NFT owner rewards for locked up NFTs in L1ERC721Bridge without becoming the owner of those NFTs.
Attacker can take locked up NFTs as flash loan without paying any fee.

## Code Snippet
https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts-bedrock/contracts/L1/L1ERC721Bridge.sol#L99-L102

## Tool used
Manual Review

## Recommendation
use the same pattern as `StandardBridge._initiateBridgeERC20()` which transfers tokens first and then set the value of the `deposits[][]`:
```solidity
    function _initiateBridgeERC20(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) internal {
        if (_isOptimismMintableERC20(_localToken)) {
            require(
                _isCorrectTokenPair(_localToken, _remoteToken),
                "StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
            );

            OptimismMintableERC20(_localToken).burn(_from, _amount);
        } else {
            IERC20(_localToken).safeTransferFrom(_from, address(this), _amount);
            deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] + _amount;
        }
........
..........
```
it means the code of `L1ERC721Bridge._initiateBridgeERC721()` would be:
```solidity
 */
    function _initiateBridgeERC721(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _tokenId,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) internal override {
        require(_remoteToken != address(0), "ERC721Bridge: remote token cannot be address(0)");

        // Construct calldata for _l2Token.finalizeBridgeERC721(_to, _tokenId)
        bytes memory message = abi.encodeWithSelector(
            L2ERC721Bridge.finalizeBridgeERC721.selector,
            _remoteToken,
            _localToken,
            _from,
            _to,
            _tokenId,
            _extraData
        );

        // Lock token into bridge
        IERC721(_localToken).transferFrom(_from, address(this), _tokenId);
        deposits[_localToken][_remoteToken][_tokenId] = true;
........
........
```