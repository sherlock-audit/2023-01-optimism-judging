ladboy233

medium

# Adversary can force the caller to pay large gas fee when bridging ETH and ERC20 and ERC721 from L2 to L1 by emitting large unused event data.

## Summary

Adversary can force the caller to pay large gas fee when bridging ETH and ERC20 from L2 to L1 by emitting large unused event data.

## Vulnerability Detail

In the current implementation, when bridging the ERC20 token or ERC721or ETH token from L1 to L2 and from L2 to L1, the extraData is not used, but it is emitted as the event.

Let us trace the call of the ERC20 token bridging flow from L2 to L1

When bridging ERC20 token from L2 to L1, the _initiateWithdrawal need to be called 

```solidity
function _initiateWithdrawal(
	address _l2Token,
	address _from,
	address _to,
	uint256 _amount,
	uint32 _minGasLimit,
	bytes calldata _extraData
) internal {
	address l1Token = OptimismMintableERC20(_l2Token).l1Token();
	if (_l2Token == Predeploys.LEGACY_ERC20_ETH) {
		_initiateBridgeETH(_from, _to, _amount, _minGasLimit, _extraData);
	} else {
		_initiateBridgeERC20(_l2Token, l1Token, _from, _to, _amount, _minGasLimit, _extraData);
	}

	emit WithdrawalInitiated(l1Token, _l2Token, _from, _to, _amount, _extraData);
}
```

which calls:

```solidity
function _initiateBridgeERC20(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _amount,
	uint32 _minGasLimit,
	bytes calldata _extraData
) internal {
	if (_isOptimismMintableERC20(_localToken)) {
		require(
			_isCorrectTokenPair(_localToken, _remoteToken),
			"StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
		);

		OptimismMintableERC20(_localToken).burn(_from, _amount);
	} else {
		IERC20(_localToken).safeTransferFrom(_from, address(this), _amount);
		deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] + _amount;
	}W

	emit ERC20BridgeInitiated(_localToken, _remoteToken, _from, _to, _amount, _extraData);

	MESSENGER.sendMessage(
		address(OTHER_BRIDGE),
		abi.encodeWithSelector(
			this.finalizeBridgeERC20.selector,
			// Because this call will be executed on the remote chain, we reverse the order of
			// the remote and local token addresses relative to their order in the
			// finalizeBridgeERC20 function.
			_remoteToken,
			_localToken,
			_from,
			_to,
			_amount,
			_extraData
		),
		_minGasLimit
	);
}
```

On corresponding L1 side, the function call needs to be call to settle the L1 transaction.

```solidity
function finalizeBridgeERC20(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _amount,
	bytes calldata _extraData
) public onlyOtherBridge {
	if (_isOptimismMintableERC20(_localToken)) {
		require(
			_isCorrectTokenPair(_localToken, _remoteToken),
			"StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
		);

		OptimismMintableERC20(_localToken).mint(_to, _amount);
	} else {
		deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] - _amount;
		IERC20(_localToken).safeTransfer(_to, _amount);
	}

	emit ERC20BridgeFinalized(_localToken, _remoteToken, _from, _to, _amount, _extraData);
}
```

note the event emission:

```solidity
emit ERC20BridgeFinalized(_localToken, _remoteToken, _from, _to, _amount, _extraData);
}
```

whatever is submitted as extraData in L2 is also emited on L1, consider that transaction gas fee is cheap in L2 when emitting large data, fi the extraData byte string is long, event emission can be very costly, and the adversary can force the caller to pay large gas fee when settle the transaction in L1 that is bridged from L2.

As shown in the Coded POC, if the emitted extraData is large and long, the transaction can quickly run out of gas.

https://drive.google.com/file/d/1ls3B1zA0YJQQ-p0JwBpjZv0xQJLsP_W9/view?usp=sharing

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.12;
import "forge-std/Test.sol";
import "forge-std/console.sol";

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "../src/Mock/NormalERC20.sol";

contract Bridge {

    event Deposit(bytes data);

    function gasBomb(bytes memory data) public {
        emit Deposit(data);
    }

}

contract POCTest is Test {
    
    // hacker address
    address hacker = vm.addr(1);
    NormalERC20 token;
    Bridge bridge;

    function setUp() public {
        token = new NormalERC20();
        bridge = new Bridge();
    }

    function testReaonsableGasOne() public {
        bytes memory data = "hihiihihihi";
        for(uint256 i; i < 10; ++i) {
            data = abi.encode(data, address(0));
        }
        bridge.gasBomb(data);
    }

    function testReaonsableGasTwo() public {
        bytes memory data = "hihiihihihi";
        for(uint256 i; i < 100; ++i) {
            data = abi.encode(data, address(0));
        }
        bridge.gasBomb(data);
    }

    function testUnboundedGas() public {
        bytes memory data = "hihiihihihi";
        for(uint256 i; i < 1000000; ++i) {
            data = abi.encode(data, address(0));
        }
        bridge.gasBomb(data);
    }


}
```

If run:

```solidity
forge test --match Gas
```

the output is:

```solidity
[⠘] Compiling 1 files with 0.8.17
[⠊] Solc 0.8.17 finished in 1.67s
Compiler run successful

Running 3 tests for test/POC.t.sol:POCTest
[PASS] testReaonsableGasOne() (gas: 35184)
[PASS] testReaonsableGasTwo() (gas: 1704637)
[FAIL. Reason: EvmError: OutOfGas] testUnboundedGas() (gas: 9223372036854754743)
Test result: FAILED. 2 passed; 1 failed; finished in 439.00ms

Failing tests:
Encountered 1 failing test in test/POC.t.sol:POCTest
[FAIL. Reason: EvmError: OutOfGas] testUnboundedGas() (gas: 9223372036854754743)
```

As we can see, if we generate large data and emit event, transaction can run out of gas.

The POC above also shows that abi.encode can run out of gas if encoding large input data.

The first example shows that emitting large data for ERC20 bridging and cause transaction to run out of gas and event large data is cheap in L2 but expensive in L1, same case applies to ETH bridging and ERC721 Bridging as well.

If ERC72 bridging from L2 to L1, first, the code below emit event on L2 in _initiateBridgeERC721 in L2ERC721Bridge.sol

```solidity
bytes memory message = abi.encodeWithSelector(
	L1ERC721Bridge.finalizeBridgeERC721.selector,
	remoteToken,
	_localToken,
	_from,
	_to,
	_tokenId,
	_extraData
);

// Send message to L1 bridge
// slither-disable-next-line reentrancy-events
MESSENGER.sendMessage(OTHER_BRIDGE, message, _minGasLimit);

// slither-disable-next-line reentrancy-events
emit ERC721BridgeInitiated(_localToken, remoteToken, _from, _to, _tokenId, _extraData);
```
 
and on L1, the corresponding event emission can run out of gas in finalizeBridgeERC721 in L2ERC721Bridge.sol

```solidity
// When a withdrawal is finalized on L1, the L1 Bridge transfers the NFT to the
// withdrawer.
IERC721(_localToken).safeTransferFrom(address(this), _to, _tokenId);

// slither-disable-next-line reentrancy-events
emit ERC721BridgeFinalized(_localToken, _remoteToken, _from, _to, _tokenId, _extraData);
```

When bridging ETH from L2 to L1, the function initiateWithdrawal is first called in L2ToL2MessagePasser.sol

```solidity
function initiateWithdrawal(
	address _target,
	uint256 _gasLimit,
	bytes memory _data
) public payable {
	bytes32 withdrawalHash = Hashing.hashWithdrawal(
		Types.WithdrawalTransaction({
			nonce: messageNonce(),
			sender: msg.sender,
			target: _target,
			value: msg.value,
			gasLimit: _gasLimit,
			data: _data
		})
	);

	sentMessages[withdrawalHash] = true;

	emit MessagePassed(
		messageNonce(),
		msg.sender,
		_target,
		msg.value,
		_gasLimit,
		_data,
		withdrawalHash
	);

	unchecked {
		++msgNonce;
	}
}
```

Then when withdrawal transaction is settled in L1, the code below can run out of gas in proveWithdrawalTransaction and finalizeWithdrawalTransaction

```solidity
// Load the ProvenWithdrawal into memory, using the withdrawal hash as a unique identifier.
bytes32 withdrawalHash = Hashing.hashWithdrawal(_tx);
ProvenWithdrawal memory provenWithdrawal = provenWithdrawals[withdrawalHash];
```

which calls:

```solidity
/**
 * @notice Derives the withdrawal hash according to the encoding in the L2 Withdrawer contract
 *
 * @param _tx Withdrawal transaction to hash.
 *
 * @return Hashed withdrawal transaction.
 */
function hashWithdrawal(Types.WithdrawalTransaction memory _tx)
	internal
	pure
	returns (bytes32)
{
	return
		keccak256(
			abi.encode(_tx.nonce, _tx.sender, _tx.target, _tx.value, _tx.gasLimit, _tx.data)
		);
}
```

As we already shown on POC, abi.enode run out of gas if _tx.data is large when adversary emit large unused data in L2 and force caller in L1 to pay large gas when settling the ETH withdrawal transaction.

## Impact

If an adversary emit large and inrelevant in L2, the caller is forced to pay gas fee to emit large and inrelevant data in L1 when settling the transaction.

## Code Snippet

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L2/L2StandardBridge.sol#L150-L178

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1StandardBridge.sol#L222-L241

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L323-L346

## Tool used

Manual Review

## Recommendation

We recommend the protocol limit the event emission data size, also, the event data is not used anyway. 