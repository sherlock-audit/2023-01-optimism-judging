ladboy233

medium

# NFT bridge transaction can fail and result in loss of NFT ownership

## Summary

NFT bridge transaction can fail and result in loss of token.

## Vulnerability Detail

Let us see the NFTbridging flow.

If user wants to bridge NFT from L1 to L2, the function below in L1ERC721Bridge.sol is called:

```solidity
/**
 * @inheritdoc ERC721Bridge
 */
function _initiateBridgeERC721(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _tokenId,
	uint32 _minGasLimit,
	bytes calldata _extraData
) internal override {
	require(_remoteToken != address(0), "ERC721Bridge: remote token cannot be address(0)");

	// Construct calldata for _l2Token.finalizeBridgeERC721(_to, _tokenId)
	bytes memory message = abi.encodeWithSelector(
		L2ERC721Bridge.finalizeBridgeERC721.selector,
		_remoteToken,
		_localToken,
		_from,
		_to,
		_tokenId,
		_extraData
	);

	// Lock token into bridge
	deposits[_localToken][_remoteToken][_tokenId] = true;
	IERC721(_localToken).transferFrom(_from, address(this), _tokenId);

	// Send calldata into L2
	MESSENGER.sendMessage(OTHER_BRIDGE, message, _minGasLimit);
	emit ERC721BridgeInitiated(_localToken, _remoteToken, _from, _to, _tokenId, _extraData);
}
```

the NFT is transferred to L1ERC721Bridge.sol

```solidity
// Send calldata into L2
MESSENGER.sendMessage(OTHER_BRIDGE, message, _minGasLimit);
```

Then in L2, the function below is called to settle the cross-chain NFT transfer.

```solidity
function finalizeBridgeERC721(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _tokenId,
	bytes calldata _extraData
) external onlyOtherBridge {
	require(_localToken != address(this), "L2ERC721Bridge: local token cannot be self");

	// Note that supportsInterface makes a callback to the _localToken address which is user
	// provided.
	require(
		ERC165Checker.supportsInterface(_localToken, type(IOptimismMintableERC721).interfaceId),
		"L2ERC721Bridge: local token interface is not compliant"
	);

	require(
		_remoteToken == IOptimismMintableERC721(_localToken).remoteToken(),
		"L2ERC721Bridge: wrong remote token for Optimism Mintable ERC721 local token"
	);

	// When a deposit is finalized, we give the NFT with the same tokenId to the account
	// on L2. Note that safeMint makes a callback to the _to address which is user provided.
	IOptimismMintableERC721(_localToken).safeMint(_to, _tokenId);

	// slither-disable-next-line reentrancy-events
	emit ERC721BridgeFinalized(_localToken, _remoteToken, _from, _to, _tokenId, _extraData);
}
```

Note that the assumption is that: if there is cross-chain NFT request from L1 to and L2 and once the deposit event is emitted in L1, if the finalizeBridgeERC721 fail for some reason, there is no way to recover the ownership of the NFT in L1, which lock the L1 NFT token,

Can the finalizeBridgeERC721 NFT revert? There is code to validate that the remoteToken is a complaint OptimismMintableERC721 NFT.

```solidity
// Note that supportsInterface makes a callback to the _localToken address which is user
// provided.
require(
	ERC165Checker.supportsInterface(_localToken, type(IOptimismMintableERC721).interfaceId),
	"L2ERC721Bridge: local token interface is not compliant"
);

require(
	_remoteToken == IOptimismMintableERC721(_localToken).remoteToken(),
	"L2ERC721Bridge: wrong remote token for Optimism Mintable ERC721 local token"
);
```

However, the validation is not sufficient beause of the safeMint function call

```solidity
// When a deposit is finalized, we give the NFT with  tokenId to the account
// on L2. Note that safeMint makes a callback to the _to address which is user provided.
IOptimismMintableERC721(_localToken).safeMint(_to, _tokenId);
```

OptimismMintableERC721 inherit from Openzeppelin's ERC721 implementation, and once safeMint is called, _checkOnERC721Received is called.

```solidity
function _safeMint(address to, uint256 tokenId, bytes memory data) internal virtual {
	_mint(to, tokenId);
	require(
		_checkOnERC721Received(address(0), to, tokenId, data),
		"ERC721: transfer to non ERC721Receiver implementer"
	);
}
```

which calls:

```solidity
function _checkOnERC721Received(
	address from,
	address to,
	uint256 tokenId,
	bytes memory data
) private returns (bool) {
	if (to.isContract()) {
		try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, data) returns (bytes4 retval) {
			return retval == IERC721Receiver.onERC721Received.selector;
		} catch (bytes memory reason) {
			if (reason.length == 0) {
				revert("ERC721: transfer to non ERC721Receiver implementer");
			} else {
				/// @solidity memory-safe-assembly
				assembly {
					revert(add(32, reason), mload(reason))
				}
			}
		}
	} else {
		return true;
	}
}
```

Basically, the recipient address (to) can take over the control flow.

The address(to) could be a smart contract does not implementation the onERC721Received interface to acknoledge the NFT mint transfer.

the smart contract (to) could intentionally revert in IERC721Receiver(to).onERC721Received external call.

the smart contract (to) could return a very large error message string to force transaction run out of gas.

address(to) also should not be address(0) as well because mint to address(0) revert.

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/3b591a48acaab78008ed39d60fbcf429a83155ca/contracts/token/ERC721/ERC721.sol#L265

```solidity
function _mint(address to, uint256 tokenId) internal virtual {
	require(to != address(0), "ERC721: mint to the zero address");
	require(!_exists(tokenId), "ERC721: token already minted");
```

When we bridge NFT from L2 to L1, same assumptions needs to hold, because when we bridge the NFT and call, L2 NFT is already burned, if the settlement transaction revert in L1, the NFT is lost.

```solidity
function _initiateBridgeERC721(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _tokenId,
	uint32 _minGasLimit,
	bytes calldata _extraData
) internal override {
```

which calls:

```solidity
// When a withdrawal is initiated, we burn the withdrawer's NFT to prevent subsequent L2
// usage
// slither-disable-next-line reentrancy-events
IOptimismMintableERC721(_localToken).burn(_from, _tokenId);
```

Then in L1, the function below is called:

```solidity
function finalizeBridgeERC721(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _tokenId,
	bytes calldata _extraData
) external onlyOtherBridge {
	require(_localToken != address(this), "L1ERC721Bridge: local token cannot be self");

	// Checks that the L1/L2 NFT pair has a token ID that is escrowed in the L1 Bridge.
	require(
		deposits[_localToken][_remoteToken][_tokenId] == true,
		"L1ERC721Bridge: Token ID is not escrowed in the L1 Bridge"
	);

	// Mark that the token ID for this L1/L2 token pair is no longer escrowed in the L1
	// Bridge.
	deposits[_localToken][_remoteToken][_tokenId] = false;

	// When a withdrawal is finalized on L1, the L1 Bridge transfers the NFT to the
	// withdrawer.
	IERC721(_localToken).safeTransferFrom(address(this), _to, _tokenId);

	// slither-disable-next-line reentrancy-events
	emit ERC721BridgeFinalized(_localToken, _remoteToken, _from, _to, _tokenId, _extraData);
}
```

However, the usage of safeTransferFrom also calls _checkOnERC721Received, if the address(to) is the smart contract that does not implementation onERC721Received interface, L1 transaction revert.

If address(to) intentionally or unintentionally revert the transaction in onERC721Received hook check, transaction transaction.

Again, address(to) and event large event that contains a lot of bytes string and return large error message force the caller pay large gas fee.

Given that finalizeBridgeERC721 does not keep track if the transaction already failed on-chain, such failture can be used to drain the gas from the caller's wallet, the caller may keep calling the finalizeBridgeERC721 function that is guaranteed to revert.

## Impact

NFT bridge transaction can fail and result in loss of NFT ownership

## Code Snippet

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1ERC721Bridge.sol#L45-L107

## Tool used

Manual Review

## Recommendation

We recommend the protocol use _mint and transferFrom to avoid handling the external call to address(to), and keep track of if the finalizeBridgeERC721 is already failed to make sure the caller do not repeatedly call a failed transaction.

Also, a NFT recovery stage should be implemented to avoid the loss of ownership.

For example, If user bridge NFT from L1 to L2 and in L2, the NFT cannot be minted on L2, user should be able to withdraw the NFT on L1, if user bridge NFT from L2 to L1, and in L1, the NFT cannot be transferred out, the NFT on L2 should be minted to user once again.
