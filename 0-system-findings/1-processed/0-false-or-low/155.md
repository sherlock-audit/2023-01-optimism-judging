
- [155.md](processed/false/155.md): #false
  - **Description:** Social engineering of bridge deposits
  - **Reason:** This describes one of many many ways that users will lose funds if
they deposit into the wrong token.

---

unforgiven

medium

# [Meduim] Users would lose their tokens if they bridge native ERC20 token to native ERC20 token in other network by StandardBridge and attacker can steal those tokens

## Summary
Contract StandardBridge is for bridging ERC20 tokens between L1 and L2 network. it supports 4 type of token bridge pairs (Native L1<-> Mintable L2) and (Mintable L1 <-> Native L2) and (Native L1 <-> Native L2) and (Mintable L1 <-> Mintable L2). if a user bridge a native token to another native token in the other network by mistake then attacker would be able to steal their tokens. attacker can lure users to make this mistake because attacker can earn a lot of funds. in the past implementations wrong token bridged are refunded and there were no incentive for attacker to lure users to make mistake but now attacker can earn reward by forcing users to make mistake.

## Vulnerability Detail
This is functions `finalizeBridgeERC20()` and `_initiateBridgeERC20()` codes in StandardBridge:
```javascript
    function _initiateBridgeERC20(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) internal {
        if (_isOptimismMintableERC20(_localToken)) {
            require(
                _isCorrectTokenPair(_localToken, _remoteToken),
                "StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
            );

            OptimismMintableERC20(_localToken).burn(_from, _amount);
        } else {
            IERC20(_localToken).safeTransferFrom(_from, address(this), _amount);
            deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] + _amount;
        }

        emit ERC20BridgeInitiated(_localToken, _remoteToken, _from, _to, _amount, _extraData);

        MESSENGER.sendMessage(
            address(OTHER_BRIDGE),
            abi.encodeWithSelector(
                this.finalizeBridgeERC20.selector,
                _remoteToken,
                _localToken,
                _from,
                _to,
                _amount,
                _extraData
            ),
            _minGasLimit
        );
    }
    
    function finalizeBridgeERC20(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _extraData
    ) public onlyOtherBridge {
        if (_isOptimismMintableERC20(_localToken)) {
            require(
                _isCorrectTokenPair(_localToken, _remoteToken),
                "StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
            );

            OptimismMintableERC20(_localToken).mint(_to, _amount);
        } else {
            deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] - _amount;
            IERC20(_localToken).safeTransfer(_to, _amount);
        }

        emit ERC20BridgeFinalized(_localToken, _remoteToken, _from, _to, _amount, _extraData);
    }
```
As you can see when localToken and remoteToken are not OptimismMintableERC20 then code would increase `deposits[localToken][remoteToken]` when bridging initialize and decrease `deposits[localToken][remoteToken]` when bridging finalize. so it's possible to bridge native token in one network to another native token in other network as long as `deposits[][]` for both bridge contract for that token pair is positive. attacker can use this to steal other users funds if they bridge their token by mistake or even lure them to bridge their token and steal their funds. to perform this attacker would perform this:
1. USER1 would bridge 1 WBTC tokens in L1 to USDC token in the L2 by mistake or by social engineering by calling `bridgeERC20(WBTC address in L1, USDC address in L2, 1)` in L1.
2. StandardBridge code would increase `deposits[WBTC][USDC]` by 1.
3. attacker would see this bridge transaction of the user and would bridge 1 USDC from L2 to WBTC token in L1 by calling `bridgeERC20(USDC address in L2, WBTC address in L1, 1)` in L2.
4. code would increase `deposits[USDC][WBTC]` by 1 and would send message to L1's StandardBridge.finalizeBridgeERC20() to withdraw 1 BTC for attacker.
5. function `finalizeBridgeERC20()` would check and see that `deposits[WBTC][USDC]` is 1 and it would successfully transfer 1 BTC to the attacker.

so attacker pair 1 USDC in the L2 and received 1 WBTC in the L1 and user lose his funds. because this attack has a lot of rewards for attacker so attacker would spend a lot of time and money to lure users to make this mistake. in this scenario both tokens were valid and legit native tokens so users would trust action of bridging tokens more easily.

## Impact
user would lose his valuable token and would receive cheap tokens.

## Code Snippet
https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L324-L345

https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L397-L436

## Tool used
Manual Review

## Recommendation
code should allow only briding of native tokens to OptimisimMintable tokens so attacker wouldn't steal users funds and there were no incentive for attacker to perform the attack.