hyh

medium

# Griefing is possible by sending dust ETH to OptimismPortal from L2 and to L2ToL1MessagePasser from L1

## Summary

Sending `1 wei` of Ether from L2 to OptimismPortal contract address is possible. OptimismPortal will send it backwards to the L1StandardBridge address on L2. As this is double transfer it provides a protocol griefing / DOS surface as the attacker costs are tiny in this case.

Sending from L1 to L2ToL1MessagePasser address is possible as well with the similar looping result, but is more costly. This constitutes a surface too as attacker will still be charged for 1 deposit, while the system perform both deposit and withdrawal, spending double amount of resources.

## Vulnerability Detail

Destinations of ETH transfers are controlled to be not bridge or messenger (both L1 and L2, this code is universal), while Portal and Passer are allowed. In the same time both bridges and Portal/Passer have deposit/withdrawal trigger via ETH receive.

Notice that messenger level _isUnsafeTarget() check also doesn't stop this as the target in the call is the corresponding bridge contract (i.e. Portal/Passer destination is in data field at this point), which is usual workflow and is allowed. 

## Impact

Attacker can DOS the system by initiating vast amount of such transfers from many different addresses, with each of them syphoning protocol resources by spending L1 gas.

As the attack can be spread wide in time the surge L1 gas price periods will be included, i.e. an attacker can do moderate, but periodical attacks and statistically they will catch costly L1 gas and make average gas cost the protocol spends across the whole attack big enough this way.

## Code Snippet

L1 and L2 standard bridges share the finalizeBridgeETH() code, that allows OptimismPortal and L2ToL1MessagePasser addresses to be in `_to`:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L295-L309

```solidity
    function finalizeBridgeETH(
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _extraData
    ) public payable onlyOtherBridge {
        require(msg.value == _amount, "StandardBridge: amount sent does not match amount required");
        require(_to != address(this), "StandardBridge: cannot send to self");
        require(_to != address(MESSENGER), "StandardBridge: cannot send to messenger");

        emit ETHBridgeFinalized(_from, _to, _amount, _extraData);

        bool success = SafeCall.call(_to, gasleft(), _amount, hex"");
        require(success, "StandardBridge: ETH transfer failed");
    }
```

Initiating the bridge doesn't control the address:

On L2:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L2/L2StandardBridge.sol#L103-L111

```solidity
    function withdrawTo(
        address _l2Token,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) external payable virtual {
        _initiateWithdrawal(_l2Token, msg.sender, _to, _amount, _minGasLimit, _extraData);
    }
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L2/L2StandardBridge.sol#L162-L178

```solidity
    function _initiateWithdrawal(
        address _l2Token,
        address _from,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) internal {
        address l1Token = OptimismMintableERC20(_l2Token).l1Token();
        if (_l2Token == Predeploys.LEGACY_ERC20_ETH) {
            _initiateBridgeETH(_from, _to, _amount, _minGasLimit, _extraData);
        } else {
            _initiateBridgeERC20(_l2Token, l1Token, _from, _to, _amount, _minGasLimit, _extraData);
        }

        emit WithdrawalInitiated(l1Token, _l2Token, _from, _to, _amount, _extraData);
    }
```

On L1:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1StandardBridge.sol#L153-L159

```solidity
    function depositETHTo(
        address _to,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) external payable {
        _initiateETHDeposit(msg.sender, _to, _minGasLimit, _extraData);
    }
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1StandardBridge.sol#L260-L268

```solidity
    function _initiateETHDeposit(
        address _from,
        address _to,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) internal {
        emit ETHDepositInitiated(_from, _to, msg.value, _extraData);
        _initiateBridgeETH(_from, _to, msg.value, _minGasLimit, _extraData);
    }
```

Sending the message:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L358-L383

```solidity
    function _initiateBridgeETH(
        address _from,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes memory _extraData
    ) internal {
        require(
            msg.value == _amount,
            "StandardBridge: bridging ETH must include sufficient ETH value"
        );

        emit ETHBridgeInitiated(_from, _to, _amount, _extraData);

        MESSENGER.sendMessage{ value: _amount }(
            address(OTHER_BRIDGE),
            abi.encodeWithSelector(
                this.finalizeBridgeETH.selector,
                _from,
                _to,
                _amount,
                _extraData
            ),
            _minGasLimit
        );
    }
```

Then, `finalizeBridgeETH` tx has `_target == OTHER_BRIDGE`:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L212-L242

```solidity
    function sendMessage(
        address _target,
        bytes calldata _message,
        uint32 _minGasLimit
    ) external payable {
        // Triggers a message to the other messenger. Note that the amount of gas provided to the
        // message is the amount of gas requested by the user PLUS the base gas value. We want to
        // guarantee the property that the call to the target contract will always have at least
        // the minimum gas limit specified by the user.
        _sendMessage(
            OTHER_MESSENGER,
            baseGas(_message, _minGasLimit),
            msg.value,
            abi.encodeWithSelector(
                this.relayMessage.selector,
                messageNonce(),
                msg.sender,
                _target,
                msg.value,
                _minGasLimit,
                _message
            )
        );

        emit SentMessage(_target, msg.sender, _message, messageNonce(), _minGasLimit);
        emit SentMessageExtension1(msg.sender, msg.value);

        unchecked {
            ++msgNonce;
        }
    }
```

Which passes relayMessage() check as that's bridge, not messenger or portal/passer:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L308-L311

```solidity
        require(
            _isUnsafeTarget(_target) == false,
            "CrossDomainMessenger: cannot send message to blocked system address"
        );
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L2/L2CrossDomainMessenger.sol#L72-L74

```solidity
    function _isUnsafeTarget(address _target) internal view override returns (bool) {
        return _target == address(this) || _target == address(Predeploys.L2_TO_L1_MESSAGE_PASSER);
    }
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1CrossDomainMessenger.sol#L67-L69

```solidity
    function _isUnsafeTarget(address _target) internal view override returns (bool) {
        return _target == address(this) || _target == address(PORTAL);
    }
```

So ETH is passed along by finalizeBridgeETH() above, and portal/passer receive() triggers another round of dust delivery with `msg.sender` being the bridge:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L140-L142

```solidity
    receive() external payable {
        depositTransaction(msg.sender, msg.value, RECEIVE_DEFAULT_GAS_LIMIT, false, bytes(""));
    }
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L2/L2ToL1MessagePasser.sol#L75-L77

```solidity
    receive() external payable {
        initiateWithdrawal(msg.sender, RECEIVE_DEFAULT_GAS_LIMIT, bytes(""));
    }
```

Supposing that some number of such calls will pass the corresponding gas requirements, i.e. gas the bridges provide will be sufficient for depositTransaction() burning, the bridging will be continued in these cases with `_to` being the bridge:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L358-L364

```solidity
    function depositTransaction(
        address _to,
        uint256 _value,
        uint64 _gasLimit,
        bool _isCreation,
        bytes memory _data
    ) public payable metered(_gasLimit) {
```

As it will be the other bridge in the finalizeBridgeETH(), i.e. `_to = other_bridge_address`, it will be delivered:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L295-L309

```solidity
    function finalizeBridgeETH(
        address _from,
        address _to,
        uint256 _amount,
        bytes calldata _extraData
    ) public payable onlyOtherBridge {
        require(msg.value == _amount, "StandardBridge: amount sent does not match amount required");
        require(_to != address(this), "StandardBridge: cannot send to self");
        require(_to != address(MESSENGER), "StandardBridge: cannot send to messenger");

        emit ETHBridgeFinalized(_from, _to, _amount, _extraData);

        bool success = SafeCall.call(_to, gasleft(), _amount, hex"");
        require(success, "StandardBridge: ETH transfer failed");
    }
```

Notice that reverting on an attempt to deliver to another bridge is stated in bridgeETHTo() description (this will still allow the DOS as that's the final step only, but tune it down somewhat), but currently finalizeBridgeETH() doesn't revert when receiver is the other bridge address:

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L195-L210

```solidity
    /**
     * @notice Sends ETH to a receiver's address on the other chain. Note that if ETH is sent to a
     *         smart contract and the call fails, the ETH will be temporarily locked in the
     *         StandardBridge on the other chain until the call is replayed. If the call cannot be
     *         replayed with any amount of gas (call always reverts), then the ETH will be
     *         permanently locked in the StandardBridge on the other chain. ETH will also
     *         be locked *if the receiver is the other bridge, because finalizeBridgeETH will revert
     *         in that case.*
     *
     * @param _to          Address of the receiver.
     * @param _minGasLimit Minimum amount of gas that the bridge can be relayed with.
     * @param _extraData   Extra data to be sent with the transaction. Note that the recipient will
     *                     not be triggered with this data, but it will be emitted and can be used
     *                     to identify the transaction.
     */
    function bridgeETHTo(
```

This way the whole operation ends up with L1StandardBridge's address credited with `1 wei` on L2 when it was started on L2:

https://community.optimism.io/docs/developers/bedrock/public-testnets/#goerli

```md
Proxy__OVM_L1StandardBridge    0x636Af16bf2f682dD3109e60102b8E1A089FedAa8
```

Similarly, L2StandardBridge's address on L1 is credited when it was started on L1.

## Tool used

Manual Review

## Recommendation

As such loops aren't a part of any user's workflow and can be utilized for DOS by deplenishing protocol resources, consider forbidding the destination to be portal or passer on the same grounds it is now forbidden to be the bridge itself in finalizeBridgeETH() (as both portal/passer and standard bridges initiate bridging on receive), for example control it in _initiateBridgeETH():

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L38-L43

```solidity
    /**
-    * @custom:legacy
-    * @custom:spacer messenger
-    * @notice Spacer for backwards compatibility.
+    * @notice Corresponding passer on the other domain.
     */
-   address private spacer_0_0_20;
+   address public immutable OTHER_PASSER;
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L157-L164

```solidity
    /**
     * @param _messenger   Address of CrossDomainMessenger on this network.
     * @param _otherBridge Address of the other StandardBridge contract.
+    * @param _otherPasser Address of the other Portal or Passer contract.
     */
    constructor(address payable _messenger, address payable _otherBridge, address payable _otherPasser) {
        MESSENGER = CrossDomainMessenger(_messenger);
        OTHER_BRIDGE = StandardBridge(_otherBridge);
+       OTHER_PASSER = _otherPasser;
    }
```

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L358-L383

```solidity
    function _initiateBridgeETH(
        address _from,
        address _to,
        uint256 _amount,
        uint32 _minGasLimit,
        bytes memory _extraData
    ) internal {
        require(
            msg.value == _amount,
            "StandardBridge: bridging ETH must include sufficient ETH value"
        );

+       require(_to != address(OTHER_BRIDGE), "StandardBridge: can't send to another bridge");
+       require(_to != OTHER_PASSER,          "StandardBridge: can't send to another passer");

        emit ETHBridgeInitiated(_from, _to, _amount, _extraData);

        MESSENGER.sendMessage{ value: _amount }(
            address(OTHER_BRIDGE),
            abi.encodeWithSelector(
                this.finalizeBridgeETH.selector,
                _from,
                _to,
                _amount,
                _extraData
            ),
            _minGasLimit
        );
    }
```