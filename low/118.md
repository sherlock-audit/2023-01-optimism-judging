ladboy233

low

# ERC20 token may be mistakenly bridged as ERC721 from L1 to L2 and never able to bridge from L2 back to L1

## Summary

ERC20 token may be mistakenly bridged as ERC721 from L1 to L2 and never able to bridge from L2 back to L1

The issue is low because is requires user mistakes but it violates the implicit business requirement: ERC721Bridge is used to bridge ERC71 and should not be used to ERC20 in any  case.

## Vulnerability Detail

In the current implementation of L1ERC721Bridge.sol, _initiateBridgeERC20

```solidity
function _initiateBridgeERC721(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _tokenId,
	uint32 _minGasLimit,
	bytes calldata _extraData
) internal override {
	require(_remoteToken != address(0), "ERC721Bridge: remote token cannot be address(0)");

	// Construct calldata for _l2Token.finalizeBridgeERC721(_to, _tokenId)
	bytes memory message = abi.encodeWithSelector(
		L2ERC721Bridge.finalizeBridgeERC721.selector,
		_remoteToken,
		_localToken,
		_from,
		_to,
		_tokenId,
		_extraData
	);

	// Lock token into bridge
	deposits[_localToken][_remoteToken][_tokenId] = true;
	IERC721(_localToken).transferFrom(_from, address(this), _tokenId);

	// Send calldata into L2
	MESSENGER.sendMessage(OTHER_BRIDGE, message, _minGasLimit);
	emit ERC721BridgeInitiated(_localToken, _remoteToken, _from, _to, _tokenId, _extraData);
}
```

However, the line of code below performs the transfer

```solidity
IERC721(_localToken).transferFrom(_from, address(this), _tokenId);
```

The code means to perform a ERC721 token transfer, however, the function signature actually overlaps the ERC20 token transferFrom signature.

Both ERC721 transferFrom method and ERC20 transferFrom method has the same byte4 signature 0x23b872dd

This means that If localToken is not ERC721 but a ERC20, transfer can still go through and the ERC20 is mistakenly bridged from L1 to L2.

However, the token cannot be bridged back, because when bridged back, the function below called:

```solidity
    function finalizeBridgeERC721(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _tokenId,
        bytes calldata _extraData
    ) external onlyOtherBridge {
        require(_localToken != address(this), "L1ERC721Bridge: local token cannot be self");

        // Checks that the L1/L2 NFT pair has a token ID that is escrowed in the L1 Bridge.
        require(
            deposits[_localToken][_remoteToken][_tokenId] == true,
            "L1ERC721Bridge: Token ID is not escrowed in the L1 Bridge"
        );

        // Mark that the token ID for this L1/L2 token pair is no longer escrowed in the L1
        // Bridge.
        deposits[_localToken][_remoteToken][_tokenId] = false;

        // When a withdrawal is finalized on L1, the L1 Bridge transfers the NFT to the
        // withdrawer.
        IERC721(_localToken).safeTransferFrom(address(this), _to, _tokenId);

        // slither-disable-next-line reentrancy-events
        emit ERC721BridgeFinalized(_localToken, _remoteToken, _from, _to, _tokenId, _extraData);
    }

```

note the line of code that settle the transfer:

```solidity
IERC721(_localToken).safeTransferFrom(address(this), _to, _tokenId);
```

If the _localToken is a ERC721 token, the token has safeTransferFrom method, however, if the _localToken is ERC20 token, ERC20 token does not have safeTransferFrom method, then if the user mistakenly bridge ERC20 token from L1 to L2 as NFT, when user want to bridge the token back and get the ERC20 token back, the transaction revert in IERC721(_localToken).safeTransferFrom

as showns in the Coded POC:

https://drive.google.com/file/d/16hrvmfj9KQnnO3hp13Gd7civrTP9iu9q/view?usp=sharing

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.12;
import "forge-std/Test.sol";
import "forge-std/console.sol";

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "../src/Mock/NormalERC20.sol";

contract Bridge {

    function depositERC721(address token, uint256 id) public {
        IERC721(token).transferFrom(msg.sender, address(this), id);
    }

    function withdrawERC721(address token, uint256 id, address receiver) public {
        IERC721(token).safeTransferFrom(address(this), receiver, id);
    }

}

contract POCTest is Test {
    
    // hacker address
    address hacker = vm.addr(1);
    NormalERC20 token;
    Bridge bridge;

    function setUp() public {
        token = new NormalERC20();
        bridge = new Bridge();
    }

    function testBridgeERC20ByMistaken() public {
        token.mint(address(hacker), 100000);
        vm.startPrank(hacker);
        token.approve(address(bridge), 100000);
        bridge.depositERC721(address(token), 100000);

        vm.expectRevert();
        bridge.withdrawERC721(address(token), 100000, hacker);
    }


}
```


The POC above that deposit call for ERC20 as ERC721 works but withdrawll call for ERC20 as ERC721 revert.

We run

```solidity
forge test
```

and the output:

```solidity
Running 1 test for test/POC.t.sol:POCTest
[PASS] testBridgeERC20ByMistaken() (gas: 92140)
Test result: ok. 1 passed; 0 failed; finished in 1.27ms
```

The withdrawal revert means that the cross-chain withdraw from L2 to L1 is not able to settle, but when user start the withdraw in L2, the wrapped NFT is already burned, which result in loss of token.

```solidity
    function _initiateBridgeERC721(
        address _localToken,
        address _remoteToken,
        address _from,
        address _to,
        uint256 _tokenId,
        uint32 _minGasLimit,
        bytes calldata _extraData
    ) internal override {
        require(_remoteToken != address(0), "ERC721Bridge: remote token cannot be address(0)");

        // Check that the withdrawal is being initiated by the NFT owner
        require(
            _from == IOptimismMintableERC721(_localToken).ownerOf(_tokenId),
            "Withdrawal is not being initiated by NFT owner"
        );

        // Construct calldata for l1ERC721Bridge.finalizeBridgeERC721(_to, _tokenId)
        // slither-disable-next-line reentrancy-events
        address remoteToken = IOptimismMintableERC721(_localToken).remoteToken();
        require(
            remoteToken == _remoteToken,
            "L2ERC721Bridge: remote token does not match given value"
        );

        // When a withdrawal is initiated, we burn the withdrawer's NFT to prevent subsequent L2
        // usage
        // slither-disable-next-line reentrancy-events
        IOptimismMintableERC721(_localToken).burn(_from, _tokenId);
```

note that the ERC721 token is burned when initing a withdraw in L2, so if the token is burned in L2 but transaction is not able to settle in L1, this is a issue.

```solidity
IOptimismMintableERC721(_localToken).burn(_from, _tokenId);
```

## Impact

ERC20 token may be mistakenly bridged as ERC721 from L1 to L2 and never able to bridge from L2 back to L1

## Code Snippet

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1ERC721Bridge.sol#L45-L107

## Tool used

Manual Review

## Recommendation

We recommend the protocol use safeTransferFrom instead of transferFrom when init the bridge ERC721 transaction.
