ladboy233

medium

# ERC20 token bridge does not support token with different decimals

## Summary

ERC20 token bridge does not support token with different decimals

## Vulnerability Detail

In the current implementation:

User can perform ERC20 cross-chain transfer via token bridge.

Let us just say user wants to perform a cross-chain token from L1 to L2

first, we need to call depositERC20 in L1StandardBridge

```solidity
function depositERC20(
	address _l1Token,
	address _l2Token,
	uint256 _amount,
	uint32 _minGasLimit,
	bytes calldata _extraData
) external virtual onlyEOA {
	_initiateERC20Deposit(
		_l1Token,
		_l2Token,
		msg.sender,
		msg.sender,
		_amount,
		_minGasLimit,
		_extraData
	);
}
```

which calls:

```solidity
function _initiateERC20Deposit(
	address _l1Token,
	address _l2Token,
	address _from,
	address _to,
	uint256 _amount,
	uint32 _l2Gas,
	bytes calldata _data
) internal {
	// When a deposit is initiated on L1, the L1 Bridge transfers the funds to itself for future
	// withdrawals. The use of safeTransferFrom enables support of "broken tokens" which do not
	// return a boolean value.
	// slither-disable-next-line reentrancy-events, reentrancy-benign
	IERC20(_l1Token).safeTransferFrom(_from, address(this), _amount);

	// Construct calldata for _l2Token.finalizeDeposit(_to, _amount)
	bytes memory message = abi.encodeWithSelector(
		IL2ERC20Bridge.finalizeDeposit.selector,
		_l1Token,
		_l2Token,
		_from,
		_to,
		_amount,
		_data
	);

	// Send calldata into L2
	// slither-disable-next-line reentrancy-events, reentrancy-benign
	sendCrossDomainMessage(l2TokenBridge, _l2Gas, message);

	// slither-disable-next-line reentrancy-benign
	deposits[_l1Token][_l2Token] = deposits[_l1Token][_l2Token] + _amount;

	// slither-disable-next-line reentrancy-events
	emit ERC20DepositInitiated(_l1Token, _l2Token, _from, _to, _amount, _data);
}
```

the code first transfer the token into L1StandardBridge.sol

```solidity
IERC20(_l1Token).safeTransferFrom(_from, address(this), _amount);
```

Then we construct the calldata:

```solidity
// Construct calldata for _l2Token.finalizeDeposit(_to, _amount)
bytes memory message = abi.encodeWithSelector(
	IL2ERC20Bridge.finalizeDeposit.selector,
	_l1Token,
	_l2Token,
	_from,
	_to,
	_amount,
	_data
);
```

Then we send message and update token balance.

```solidity
// Send calldata into L2
// slither-disable-next-line reentrancy-events, reentrancy-benign
sendCrossDomainMessage(l2TokenBridge, _l2Gas, message);

// slither-disable-next-line reentrancy-benign
deposits[_l1Token][_l2Token] = deposits[_l1Token][_l2Token] + _amount;

// slither-disable-next-line reentrancy-events
emit ERC20DepositInitiated(_l1Token, _l2Token, _from, _to, _amount, _data);
```

Then on L2, finalizeDeposit is called:

```solidity
function finalizeDeposit(
	address _l1Token,
	address _l2Token,
	address _from,
	address _to,
	uint256 _amount,
	bytes calldata _extraData
) external payable virtual {
	if (_l1Token == address(0) && _l2Token == Predeploys.LEGACY_ERC20_ETH) {
		finalizeBridgeETH(_from, _to, _amount, _extraData);
	} else {
		finalizeBridgeERC20(_l2Token, _l1Token, _from, _to, _amount, _extraData);
	}

	emit DepositFinalized(_l1Token, _l2Token, _from, _to, _amount, _extraData);
}
```

then finalizeBridgeERC20 token is called:

```solidity
function finalizeBridgeERC20(
	address _localToken,
	address _remoteToken,
	address _from,
	address _to,
	uint256 _amount,
	bytes calldata _extraData
) public onlyOtherBridge {
	if (_isOptimismMintableERC20(_localToken)) {
		require(
			_isCorrectTokenPair(_localToken, _remoteToken),
			"StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
		);

		OptimismMintableERC20(_localToken).mint(_to, _amount);
	} else {
		deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] - _amount;
		IERC20(_localToken).safeTransfer(_to, _amount);
	}

	emit ERC20BridgeFinalized(_localToken, _remoteToken, _from, _to, _amount, _extraData);
}
```

note the line of code:

```solidity
		OptimismMintableERC20(_localToken).mint(_to, _amount);
```

Once the equal amount of token is minted on L2, the deposit is deemed complete.

However, the OptimismMintableERC20 inherit from ERC20 contract

```solidity
contract OptimismMintableERC20 is IOptimismMintableERC20, ILegacyMintableERC20, ERC20 {
```

ERC20 contract assume that the token decimal is 18 on L2.

However, the token bridged from L1 may be not have 18 decimals.

According to

https://github.com/d-xo/weird-erc20#low-decimals

Some tokens have low decimals (e.g. USDC has 6). Even more extreme, some tokens like Gemini USD only have 2 decimals.

Let us assume a token A with 6 decimals is bridged from L1 to L2.

User wants to bridge 100 token A from L1 to L2.

In L1, the 100 * 10 ** 6 WEI amount of token A is locked in L1 bridge.

In L2, the 100 * 10 ** 6 WEI amount of token A is minted in L2, however, 100 *  10 ** 6 WEI amount of token A is far from even the one token A (which is 10 ** 18 WEI).

External protocol that integrate with the L2 bridged token A may have to assume one unit of token for token A is 10 ** 18 WEI and perform trade based on this assumption.

In this case, 100 unit of token A in 6 decimals in L1 becomes 10 ** 8 / 10 ** 18 (1e-10 amount of) bridged token A in 18 decimals.

## Impact

Loss fund because of the lack of decimal token scaling when bridging ERC20 token.

## Code Snippet

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L1/L1StandardBridge.sol#L171-L190

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/L2/L2StandardBridge.sol#L112-L140

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L323-L346

https://github.com/sherlock-audit/2023-01-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/OptimismMintableERC20.sol#L16

## Tool used

Manual Review

## Recommendation

We bridging token, If the token A in L1 has 6 decimals, and user bridge 100 unit of token A from L1 to L2, in L2, 100 * 10 ** 18 amount of token should be minted instead of minting 100 * 10 ** 6.

Or make sure the L2 bridged token's decimal match the L1's token decimal to avoid such issue.
